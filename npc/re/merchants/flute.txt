//===== Hercules Script ======================================
//= Falcon & Wolf Flute Trader
//===== By: ==================================================
//= Masao, Muad_Dib, Ziu
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= [Official Conversion]
//= Sells Falcon Flute and Wolf Flute.
//===== Additional Comments: =================================
//= 1.0 First Version. [Masao]
//= 1.1 Added Baby Hunter and Baby Ranger check. bugreport:5728 [Masao]
//= 1.2 Added Wolf Flute trader, optimized. [Euphy]
//= 1.2a Fixed some conditions. [Euphy]
//= 1.3 kRO Official Texts. [nyalin]
//============================================================

-	script	Falcon Flute Trader::fflute	-1,{
	mes "[팔콘 피리 상인]";
	if (BaseJob == Job_Hunter) {
		mes "팔콘이 필요해?";
		mes "이 마법의 피리만 있다면 어디서든 팔콘을 계속 부를 수 있어!";
		next;
		mes "[팔콘 피리 상인]";
		mes "신기하지? 그렇지? 하하하!";
		next;
		mes "[팔콘 피리 상인]";
		mes "바드인 내가 어째서 팔콘을 부르는 피리를 파느냐고?";
		mes "난 피리를 불 줄 모르거든. 하하하!";
		next;
		mes "[팔콘 피리 상인]";
		mes "물폰 <^ff0000팔콘리 마스터리^000000>스킬도 없어서 팔콘을 다룰 줄도 몰라. 이런 상태에서";
		mes "팔콘을 불렀다간 내 모자가 남아나지 않을 지도 모르지.";
		next;
		mes "[팔콘 피리 상인]";
		mes "하지만 공짜로 줄 순 없어!";
		mes "나도 먹고 살아야지!";
		next;
		mes "[팔콘 피리 상인]";
		mes "팔콘 피리가 필요하다면 12,500 z에 팔게. 너무 비싸다고?";
		next;
		if (eaclass()&EAJL_THIRD) {
			mes "[팔콘 피리 상인]";
			mes "그래 인심 썼다. 10,000 z에 판다!";
			mes "더 이상은 안돼!";
			.@price = 10000;
			next;
		} else
			.@price = 12500;
		if(select("사지 않는다:팔콘 피리를 산다") == 1) {
			mes "[팔콘 피리 상인]";
			mes "그거 아쉽군. 필요하면 다시 찾아와!";
			close;
		}
		if (Zeny >= .@price) {
			if (countitem(12848) >= 1) {
				mes "[팔콘 피리 상인]";
				mes "As I can see you already have a Falcon Flute!";
				mes "Don't worry, my young friend, once you've purchased the Falcon Flute you won't need to do so again";
				mes "since the Falcon Flute won't disappear upon usage, isn't that great?";
				close;
			}
			mes "[팔콘 피리 상인]";
			mes "자, 이 피리를 받아.";
			mes "그 피리를 불면 언제든 팔콘을 부를 수 있을 거야.";
		  	Zeny -= .@price;
			getitem 12848,1; //Falcon_Flute
			next;
			mes "[팔콘 피리 상인]";
			mes "다시 한번 말하지만";
			mes "<^ff0000팔콘리 마스터리^000000> 스킬이 없으면 팔콘을 부를 수 없으니 알아둬!";
			close;
		}
		mes "[팔콘 피리 상인]";
		mes "I'm sorry but you don't have enough money to buy a Falcon Flute, please come back when you have more money!";
		close;
	}
	if (BaseJob == Job_Archer) {
		mes "Hello, young one!";
		mes "Currently I can't help you,";
		mes "but why don't you return to me when you've become a great Hunter and learned how to handle Falcons properly?";
		close;
	}
	mes "Isn't it a beautiful day today?";
	close;
}
hu_in01,386,306,3	duplicate(fflute)	팔콘 피리 상인#fft	1_M_BARD
pay_arche,91,134,3	duplicate(fflute)	팔콘 피리 상인#fft2	1_M_BARD

tur_dun01,89,170,5	script	Expert Flute Crafter	1_M_ORIENT01,{
	if (Class == Job_Ranger || Class == Job_Ranger_T || Class == Job_Baby_Ranger) {
		if (countitem(6124)) {
			mes "[Expert Flute Crafter]";
			mes "The Wolf Flute you have is crafted by myself, with wood.";
			mes "You can come back here and find me if you lose your Wolf flute!";
			next;
			mes "[Expert Flute Crafter]";
			mes "The flute can only be crafted by me.";
			mes "It is just a little matter for me to craft another flute!!";
			close;
		}
		mes "[Expert Flute Crafter]";
		mes "Do you need anything?";
		mes "A new Wolf Flute?";
		next;
		switch(select("Please give me a new Wolf Flute.:No.")) {
		case 1:
			if (countitem(7150) && Zeny >= 100000) {
				mes "[Expert Flute Crafter]";
				mes "Have you brought all the materials?";
				mes "Wow! All your materials are not bad!";
				mes "That's good enough.";
				next;
				mes "[Expert Flute Crafter]";
				mes "Here's the new Wolf Flute.";
				mes "Hope you are ready to use it.";
				delitem 7150,1; //Bamboo_Cut
				Zeny -= 100000;
				getitem 6124,1; //Wolf's_Flute
				close;
			}
			mes "[Expert Flute Crafter]";
			mes "If you want to craft a new Wolf Flute, I need you to bring me ^4d4dff1 Bamboo Cut and 100,000 zeny.^000000";
			next;
			mes "[Expert Flute Crafter]";
			mes "^4d4dffBamboo Cut can be obtained from Kapha or Karakasa.^000000";
			close;
		case 2:
			mes "[Expert Flute Crafter]";
			mes "If you do not summon a wolf, then you don't need the Wolf Flute.";
			mes "You can come back here and find me if you change your mind.";
			mes "Isn't that right?";
			close;
		}
	}
	mes "[Expert Flute Crafter]";
	mes "Eeehh? You are not a Ranger?";
	mes "Then this flute is totally useless for you!";
	close;
}
