//===== Hercules Script ======================================
//= Eden Group Quests - Common NPCs
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.9
//===== Description: =========================================
//= Eden Group Headquarter NPC's.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Eden Group Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 kRO Official Texts. [nyalin]
//============================================================

moc_para01,27,35,5	script	사무담당 라임 이베노르	4_F_EDEN_OFFICER,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you loose some weight. -";
		close;
	}
	cutin "LAIME_EVENOR01",2;
	mes "[라임 이베노르]";
	mes "꿈과 낭만을 좇는 이들이여 그대들의 낙원은 연인의 품안도 아스가르드의 나팔소리에 의한 것도 아닌 바로 이곳 낙원단과 함께 함에 있습니다.";
	mes "반갑습니다. 무엇을 도와드릴까요?";
	next;
	while (1) {
		switch (select("낙원단이란:낙원단 등록:의뢰 등록:의뢰 검색:대화 종료")) {
		case 1:
			mes "[라임 이베노르]";
			mes "낙원단에서는 미드가르드 각지의 어려움에 처한 이들의 의뢰를 접수하고 문제를 해결해주는 일을 하고 있습니다.";
			next;
			mes "[라임 이베노르]";
			mes "의뢰주로부터 연락이 오면 우리는 해당 의뢰 내용을 게시하고 낙원단원들에게 소개를 하죠.";
			next;
			mes "[라임 이베노르]";
			mes "낙원단원들은 각자 의뢰를 살펴보고 자신에게 맞는 조건의 일을 찾아 해결할 수 있습니다.";
			mes "물론 의뢰인으로부터 정당한 보수도 받을 수 있지요.";
			next;
			mes "[라임 이베노르]";
			mes "낙원단에서의 의뢰활동을 통해 초급자는 이 낯설고도 넓은 세계에 적응하고 살아가는 방법을 배울 수 있으며, 중급자 이상은 세계를";
			mes "여행하며 여러 인연을 쌓아갈 수 있습니다. 물론 전문가로서의 명예와 재화도 그에 따라 축적될 것입니다.";
			next;
			break;
		case 2:
			if (countitem(22508) < 1) {
				mes "[라임 이베노르]";
				mes "간단한 등록절차를 통해 누구나 낙원단의 일원이 될 수 있습니다.";
				mes "낙원단과 함께 하시겠습니까?";
				next;
				switch (select("네:아니오")) {
				case 1:
					mes "[라임 이베노르]";
					mes "예, 그럼 여기에 이름을 적어주세요.";
					input .@input$;
					next;
					mes "[라임 이베노르]";
					mes "다 적으셨나요?";
					mes "어디보자.";
					next;
					mes "[라임 이베노르]";
					mes "이름이 ^3131FF보"+.@input$+"바^000000...? 맞습니까?";
					mes "에? 아니라고요?";
					next;
					emotion e_swt;
					mes "[라임 이베노르]";
					mes "하지만 여기 써있는 건 분명...";
					mes "흐음, 분명 꽤 난해한 글씨긴 합니다만.";
					next;
					mes "[라임 이베노르]";
					mes "이름을 다시 적어주시겠습니까? 이번엔 ^3131FF또박또박^000000 제대로 부탁드립니다.";
					input .@input$;
					next;
					mes "[라임 이베노르]";
					mes "아아, ^3131FF"+strcharinfo(0)+"^000000 였군요.";
					mes "이번엔 제대로 확인했습니다.";
					next;
					emotion e_no1;
					mes "[라임 이베노르]";
					mes "낙원단원 명단에 당신의 이름을 기입했습니다.";
					mes "낙원단에 오신 것을 환영합니다!";
					next;
					mes "[라임 이베노르]";
					mes "앞으로 낙원단원으로서 여러 의뢰를 수행하며 훌륭한 해결사로서의 선전을 기원합니다.";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes "[라임 이베노르]";
					mes "낙원단에 대해 아직 궁금한 점이라도 남으셨습니까?";
					next;
					break;
				}
			} else {
				mes "[라임 이베노르]";
				mes "이미 낙원단에 가입된 상태입니다.";
				next;
			}
			break;
		case 3:
			mes "[라임 이베노르]";
			mes "낙원단에 요청하고자 하는 의뢰가 있으십니까?";
			next;
			if(select("네:아니오") == 1) {
				mes "[라임 이베노르]";
				mes "자, 그럼 여기 서류에 이름을 적어주세요.";
				input .@input$;
				next;
				mes "[라임 이베노르]";
				mes "^3131FF"+strcharinfo(0)+"^000000.. 이 이름이 맞습니까?";
				mes "흠. 글씨를 잘 못쓰시는군요.";
				mes "알아보기가...";
				next;
				mes "[라임 이베노르]";
				mes "^3131FF"+strcharinfo(0)+"^000000님, 의뢰하고자 하는 내용을 알려주세요.";
				mes "어느 지역에서 수행하게 되는 의뢰입니까?";
				input .@input$;
				next;
				mes "[라임 이베노르]";
				mes "^3131FF수행처: "+.@input$+"^000000";
				mes "흠.. 일단 기입해 두도록 하죠.";
				next;
				mes "[라임 이베노르]";
				mes "어떤 일을 수행하게 되는지 의뢰내용을 개략적으로 적어주세요.";
				mes "'포링 10마리 처치'이런 식으로요.";
				input .@input$;
				next;
				mes "[라임 이베노르]";
				mes "다 쓰셨습니까?";
				mes "어디보자.";
				next;
				cutin "LAIME_EVENOR03",2;
				mes "[라임 이베노르]";
				mes "음...........";
				next;
				mes "[라임 이베노르]";
				mes "으으음..................";
				next;
				emotion e_dots;
				mes "[라임 이베노르]";
				mes "의뢰 등록 이전에 저의 의뢰를 받아보시는게 어떻겠습니까?";
				next;
				cutin "LAIME_EVENOR04",2;
				emotion e_pif;
				mes "[라임 이베노르]";
				mes "^3131FF의뢰자: 라임 이베노르^000000";
				mes "^3131FF수행내용: 글씨교정소에서 한달간 훈련^000000";
				mes "참으로 난해한 글씨로군요.";
				next;
				cutin "LAIME_EVENOR01",2;
				mes "[라임 이베노르]";
				mes "^3131FF수행내용:"+.@input$+"^000000";
				mes "어쨌든 무사히 등록된 것 같습니다.";
				next;
				mes "[라임 이베노르]";
				mes "현재 등록건수가 상당하여 게시판에 등록되기 까지는 시간이 좀 걸릴 것 같군요.";
				mes "차분히 순서를 기다려 주시면 감사하겠습니다.";
				next;
			}
			break;
		case 4:
			if (countitem(22508) > 0) {
				mes "[라임 이베노르]";
				mes "낙원단의 일원으로서 낙원단에 접수된 의뢰들을 수행하고 싶으십니까?";
				next;
				mes "[라임 이베노르]";
				mes "우측 벽면의 게시판에서 여러 종류의 의뢰들이 당신의 선택을 기다리고 있습니다.";
				mes "의뢰수행 조건과 내용을 읽어보시고 자신의 능력에 맞추어 좋은 선택을 하시길 바랍니다.";
				next;
			}
			else {
				mes "[라임 이베노르]";
				mes "우선은 낙원단 가입 등록 절차를 마친 후 이야기 하도록 하죠.";
				close2;
				cutin "",255;
				end;
			}
			break;
		case 5:
			cutin "",255;
			mes "[라임 이베노르]";
			mes "더 궁금하신 사항이 있으시면 언제든지 저를 찾아주세요.";
			close;
		}
	}
	close;
}

-	script	낙원단 공간이동사#0::eto	-1,{
	mes "[낙원단 공간이동사]";
	mes "이 세계의 모든 것이 낯설은 수련생이여 목표없이 방황중인 모험가여, 내게로 오세요!";
	mes "당신의 어려움을 타파해 줄 낙원단 등장!";
	next;
	mes "[낙원단 공간이동사]";
	mes "흐르는 시간을 주체할 수가 없다고요?";
	mes "기운은 넘치는데 할일이 없다고요?";
	mes "전문 해결사가 되고 싶습니까?";
	mes "무엇이 고민이십니까? 지금 당장 낙원단과 상담하세요!";
	next;
	switch (select("낙원단으로 이동:대화를 마친다")) {
	case 1:
		mes "[낙원단 공간이동사]";
		mes "자아, 갑시다 우리들의 아지트로!";
		nak_warp = strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[낙원단 공간이동사]";
		mes "앞날이 막막할 땐 언제든지 낙원달을 찾아주세요!";
		close;
	}
OnInit:
	questinfo 1001, QTYPE_QUEST, 0;//미사용 퀘스트
	end;
}
prontera,124,76,3	duplicate(eto)	낙원단 공간이동사#1	4_F_NOVICE
moc_ruins,68,164,3	duplicate(eto)	낙원단 공간이동사#2	4_F_NOVICE
geffen,132,66,3	duplicate(eto)	낙원단 공간이동사#3	4_F_NOVICE
alberta,124,67,3	duplicate(eto)	낙원단 공간이동사#4	4_F_NOVICE
aldebaran,133,119,5	duplicate(eto)	낙원단 공간이동사#5	4_F_NOVICE
izlude_in,68,162,1	duplicate(eto)	낙원단 공간이동사#6	4_F_NOVICE
prt_church,103,78,3	duplicate(eto)	낙원단 공간이동사#7	4_F_NOVICE
geffen_in,160,104,5	duplicate(eto)	낙원단 공간이동사#8	4_F_NOVICE
moc_prydb1,53,126,3	duplicate(eto)	낙원단 공간이동사#9	4_F_NOVICE
alberta_in,75,39,3	duplicate(eto)	낙원단 공간이동사#10	4_F_NOVICE
payon_in02,58,58,1	duplicate(eto)	낙원단 공간이동사#11	4_F_NOVICE
payon,177,111,3	duplicate(eto)	낙원단 공간이동사#12	4_F_NOVICE
que_ng,33,73,3	duplicate(eto)	낙원단 공간이동사#13	4_F_NOVICE
que_ng,144,161,5	duplicate(eto)	낙원단 공간이동사#14	4_F_NOVICE
yuno,144,189,5	duplicate(eto)	낙원단 공간이동사#15	4_F_NOVICE
rachel,125,144,3	duplicate(eto)	낙원단 공간이동사#16	4_F_NOVICE
comodo,202,151,4	duplicate(eto)	낙원단 공간이동사#17	4_F_NOVICE
hugel,93,153,4	duplicate(eto)	낙원단 공간이동사#18	4_F_NOVICE
veins,220,109,4	duplicate(eto)	낙원단 공간이동사#19	4_F_NOVICE
einbroch,250,211,4	duplicate(eto)	낙원단 공간이동사#20	4_F_NOVICE
lighthalzen,164,86,4	duplicate(eto)	낙원단 공간이동사#21	4_F_NOVICE
amatsu,100,145,5	duplicate(eto)	낙원단 공간이동사#22	4_F_NOVICE
ayothaya,221,191,3	duplicate(eto)	낙원단 공간이동사#23	4_F_NOVICE
louyang,224,107,3	duplicate(eto)	낙원단 공간이동사#24	4_F_NOVICE
gonryun,162,122,5	duplicate(eto)	낙원단 공간이동사#25	4_F_NOVICE
moscovia,209,197,5	duplicate(eto)	낙원단 공간이동사#26	4_F_NOVICE
brasilis,191,224,3	duplicate(eto)	낙원단 공간이동사#27	4_F_NOVICE
dewata,192,193,5	duplicate(eto)	낙원단 공간이동사#28	4_F_NOVICE
morocc,161,97,5	duplicate(eto)	낙원단 공간이동사#29	4_F_NOVICE
izlude,131,148,4	duplicate(eto)	낙원단 공간이동사#30	4_F_NOVICE
izlude_a,131,148,4	duplicate(eto)	낙원단 공간이동사#30::eto_iz_a	4_F_NOVICE
izlude_b,131,148,4	duplicate(eto)	낙원단 공간이동사#30::eto_iz_b	4_F_NOVICE
izlude_c,131,148,4	duplicate(eto)	낙원단 공간이동사#30::eto_iz_c	4_F_NOVICE
izlude_d,131,148,4	duplicate(eto)	낙원단 공간이동사#30::eto_iz_d	4_F_NOVICE
umbala,105,158,3	duplicate(eto)	낙원단 공간이동사#31	4_F_NOVICE
malaya,238,206,6	duplicate(eto)	낙원단 공간이동사#32	4_F_NOVICE

moc_para01,30,10,0	script	#eden_out	WARPNPC,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	WARPNPC,1,1,{
	if (countitem(22508) > 0) {
		warp "moc_para01",106,14;
		end;
	}
	mes "[라임 이베노르]";
	mes "아, 그 곳은 낙원단 전용 공간입니다.";
	mes "안으로 들어가시려면 저에게서 낙원단 가입신청서를 작성하셔야 합니다.";
	close;
}

moc_para01,16,22,7	script	늙은 모험가#eden	4_M_HUMERCHANT,{
	mes "[늙은 모험가]";
	mes "라임 이베노르인지 뭔지 저 머리에 피도 안마른 녀석이 아직 팔팔한 이 몸을 몰라뵙고 그저 '잡상인은 받지 않습니다'라잖아!?";
	next;
	mes "[늙은 모험가]";
	mes "이래뵈도 나도 낙원단에 가입하러 왔거늘 이게 무슨 푸대접인지!";
	mes "자네도 내가 잡상인 처럼 보이나?";
	close;
}

moc_para01,182,48,3	script	낙원단 요리사#eden	4_COOK,{
	.@eggf = rand(1,118);
	.@eggf_1 = .@eggf + 1;
	.@eggf_2 = .@eggf + 2;
	.@eggf_6 = .@eggf + 6;
	mes "[낙원단 요리사]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion e_sob;
	mes "[낙원단 요리사]";
	mes "아앗! 이게 몇 번째 계란후라이였지?";
	mes "으아아아앙!!! 또 처음부터 세야하잖아!!";
	next;
	emotion e_sob;
	mes "[낙원단 요리사]";
	mes "아가씨는 왜 나에게 이런 일을 의뢰랍시고 시키는거죠?";
	next;
	emotion e_sob;
	mes "[낙원단 요리사]";
	mes "계란후라이를 123개나 만들어서 뭐에 쓰시려고! 혼자 다 드실거도 아니면서 이건 절 단순히 괴롭히고 싶어서 그런게 분명해요! 으아아앙!!!";
	close;
}

moc_para01,172,28,5	script	수습 낙원단원#eden	4_M_ALCHE_D,{
	mes "[수습 낙원단원]";
	mes "내가 여기 낙원단을 와서 느낀건데";
	next;
	mes "[수습 낙원단원]";
	mes "정말 열심히 하지 않으면 안될거 같아...";
	next;
	emotion e_otl;
	mes "[수습 낙원단원]";
	mes "근데 난 파머시조차 성공해본 적이 없잖아?";
	next;
	emotion e_sob;
	mes "[수습 낙원단원]";
	mes "난 아마 안될거야...";
	close;
}

moc_para01,20,35,5	script	사무보조 니데	4_F_CHNDOCTOR,{
	mes "[니데]";
	mes "제가 낙원단에 들어온게 엊그제 같은데 생각해보면 벌써 계절이 몇번이나 바뀌었지 뭐에요?";
	mes "정말 일거리가 어찌나 많은지 시간가는 줄 모르고 있었어요.";
	next;
	mes "[니데]";
	mes "곰곰히 생각해보면 하루하루가 전쟁과도 같은 나날이었어요.";
	next;
	mes "[니데]";
	mes "^3131FF우측 식당^000000의 주방장은";
	mes "대체 음식에 향신료를 어찌나 써대는지, 로비까지 그 자극적인 냄새가 끊이질 않아 정말이지 수시로 통풍을 시켜주지 않으면 여기가 로비인지 식당인지 알수가 없을 정도에요.";
	next;
	mes "[니데]";
	mes "거기다 ^3131FF로비 북쪽^000000의 문 건너에서는 ^3131FF연무장^000000의 함성소리가 그치질 않고,";
	next;
	emotion e_ag;
	mes "[니데]";
	mes "^3131FF2층 숙소^000000에서는 아가씨의 신경질적인 비명 소리가 계단을 타고 내려와 연무장의 함성소리에 맞춰 합창을 이룬다니까요?";
	next;
	mes "[니데]";
	mes "아, 아가씨는 우리 낙원단의 보스를 말하는 거에요.";
	next;
	mes "[니데]";
	mes "대체 하루종일 뭘 하는지 꽥-이라거나 아악-이라거나 캬악-이라거나, 혹은 뭔가 펑! 하는 소리가 들리기도 하고 정말이지 알 수가 없는 분이에요.";
	next;
	emotion e_omg;
	mes "[니데]";
	mes "핫!";
	mes "쉿... 제가 이런 말을 한건 비밀이에요!";
	next;
	mes "[니데]";
	emotion e_sigh;
	mes "이베노르씨는 저래보여도 속을 알 수 없는 능구렁이같은 사람이라 몰래 아가씨에게 고자질을 할지 모른다고요.";
	next;
	emotion e_pif,0,"사무담당 라임 이베노르";
	mes "[라임 이베노르]";
	mes "저 그런 사람 아닙니다~";
	next;
	emotion e_wah;
	mes "[니데]";
	mes "어맛! 다 들렸나봐! 꺄악!";
	close;
}