//===== Hercules Script ======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 kRO Official Texts. [nyalin]
//============================================================

moc_para01,36,38,3	script	의뢰 [11 - 25]#Tuto	4_BOARD3,{
	if (countitem(22508) < 1) {
		mes "- 의뢰를 받기 위해서는 -";
		mes "- ^4d4dff'낙원단의 증표'^000000가 있어야 합니다.-";
		close;
	}
	for(.@i = 11124; .@i<11134; ++.@i)
		if (questprogress(.@i,PLAYTIME) == 2) erasequest .@i;
	for(.@i = 11114; .@i<11123; ++.@i) {
		.@state = questprogress(.@i,HUNTING);
		if (.@state == 2) {
			mes " - 진행중인 의뢰를 완료해야 - ";
			mes " - 새로운 의뢰를 받을 수 - ";
			mes " - 있습니다. - ";
			mes " - 담당자 : 스피클 - ";
			close;
		}
		else if (.@state) {
			mes " - 진행중인 의뢰를 완료해야 - ";
			mes " - 새로운 의뢰를 받을 수 - ";
			mes " - 있습니다. - ";
			mes " - 담당자 : 스피클 - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - 적당한 일거리가 보이지 - ";
		mes " - 않습니다. - ";
		close;
	}
	if (BaseLevel > 25) {
		mes " - 너무 쉬운 일들 뿐입니다. - ";
		mes " - 다른 일을 찾아보는 것이 - ";
		mes " - 좋을 것 같습니다. - ";
		close;
	}
	mes " - 초심자를 위한 간단한 -";
	mes " - 의뢰 게시판 -";
	mes " - 담당자 : 스피클 -";
	next;
	switch(select("호넷 사냥:콘도르 사냥:메뚜기의 뒷다리:웜테일 사냥:스포아 사냥:해충 박멸:무카 사냥:퍼밀리어 사냥:깃털 수집:독버섯 포자 수집:그만 둔다")) {
		case 1: callsub L_Quest,11114,"호넷 사냥"," - 프론테라 서북쪽 ^4d4dff'호넷'^000000을 - "," - 10마리 잡아오시오. - ";
		case 2: callsub L_Quest,11115,"콘도르 사냥"," - 모로크 남쪽의 ^4d4dff'콘도르'^000000를 - "," - 20마리 잡아오시오. - ";
		case 3: callsub L_Quest,11116,"메뚜기의 뒷다리"," - 프론테라 남서쪽의 ^4d4dff'로커'^000000를 - "," - 10마리 잡고 ^4d4dff메뚜기의 뒷다리^000000 - "," - 10개를 가져오시오. - ";
		case 4: callsub L_Quest,11117,"웜테일 사냥"," - 페이욘 동남쪽의 ^4d4dff'웜테일'^000000을 - "," - 20마리 잡아오시오. - ";
		case 5: callsub L_Quest,11118,"스포아 사냥"," - 페이욘 동동쪽의 ^4d4dff'스포아'^000000를 - "," - 30마리 잡아오시오. - ";
		case 6: callsub L_Quest,11119,"해충 박멸"," - 프론테라 서쪽 지하수로의 - "," - ^4d4dff도둑벌레 알^000000을 20개 없애고 - "," - ^4d4dff번데기 껍질^000000 10개를 가져오시오. - ";
		case 7: callsub L_Quest,11120,"무카 사냥"," - 프론테라 남남쪽의 ^4d4dff'무카'^000000를 - "," - 20마리 잡아오시오. - ";
		case 8: callsub L_Quest,11121,"퍼밀리어 사냥"," - 프론테라 서쪽 지하수로의 - "," - ^4d4dff퍼밀리어^000000를 20마리 잡아오시오. - ";
		case 9: callsub L_Quest,11122,"깃털 수집"," - 프론테라 남남쪽의 ^4d4dff'페코페코'^000000를 - "," - 30마리 잡아오시오. - ";
		case 10: callsub L_Quest,11123,"독버섯 포자 수집"," - 게펜 북동쪽의 ^4d4dff포이즌 스포아^000000를 - "," - 30마리 잡고 ^4d4dff독버섯 포자^000000를 - "," - 5개 모아오시오. - ";
		case 11: mes " - 게시판 열람을 구만둔다. - "; close;
	}
	end;
L_Quest:
	if (questprogress(getarg(0),PLAYTIME) == 1) {
		mes " - 오늘의 ^4d4dff"+getarg(1)+"^000000 의뢰는 - ";
		mes " - 종료 되었습니다. - ";
		mes " - 담당자 : 스피클 - ";
		close;
	}
	mes " - "+getarg(1)+" 의뢰 - ";
	for(.@i = 2; .@i<getargcount(); ++.@i)
		mes getarg(.@i);
	mes " - 담당자 : 스피클 - ";
	next;
	if(select("결정:다른일을 찾아본다.") == 2) {
		mes " - 다른 일거리를 찾아보자. - ";
		close;
	}
	for(.@i = 2; .@i<getargcount(); ++.@i)
		mes getarg(.@i);
	if (getarg(0) == 11123 && questprogress(11123,PLAYTIME) == 2)
		erasequest 11133;	// Special case for "Collect Poison Spores"
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	스피클	4_F_CHILD,{
	for(.@i = 11114; .@i<11124; ++.@i) {
		.@state = questprogress(.@i,HUNTING);
		if (.@state == 2) switch(.@i) {
			case 11114: callsub L_Quest,.@i,"호넷 사냥",900;
			case 11115: callsub L_Quest,.@i,"콘도르 사냥",1800;
			case 11116: callsub L_Quest,.@i,"메뚜기의 뒷다리",1400,940,10;
			case 11117: callsub L_Quest,.@i,"웜테일 사냥",2600;
			case 11118: callsub L_Quest,.@i,"스포아 사냥",3900;
			case 11119: callsub L_Quest,.@i,"해충 박멸",2900,915,10;
			case 11120: callsub L_Quest,.@i,"무카 사냥",3200;
			case 11121: callsub L_Quest,.@i,"퍼밀리어 사냥",3200;
			case 11122: callsub L_Quest,.@i,"깃털 수집",5400;
			case 11123: callsub L_Quest,.@i,"독버섯 포자 수집",6000,7033,5;
		}
		else if (.@state) {
			mes "[스피클]";
			mes "아직 의뢰 내용을 끝내지 못한 것 같군요.";
			mes "어려운 일은 아니니 더욱 힘내시길 바래요.";
			close;
		}
	}
	mes "[스피클]";
	mes "뭔가 할일을 찾고 있나요?";
	mes "저희 조합에서는 항상 의뢰가 잔뜩 밀려 있어 고민이랍니다.";
	next;
	mes "[스피클]";
	mes "괜찮다면 이 곳을 둘러보면서 적당한 일을 찾아 보세요.";
	close;
L_Quest:
	if (questprogress(getarg(0),HUNTING) == 2) {
		if (getarg(3,0))
			if (countitem(getarg(3)) < getarg(4)) {
				mes "[스피클]";
				mes "You don't have enough "+getitemname(getarg(3))+".";
				mes "You need to bring "+getarg(4)+" "+getitemname(getarg(3))+".";
				close;
			}
		mes "[스피클]";
		mes "음... 어디보자~";
		mes ""+getarg(1)+" 의뢰를 완료 하셨군요.";
		next;
		mes "[스피클]";
		mes "정~~말 수고하셨어요.";
		mes "누군지는 말씀 드릴 수 없지만 의뢰인분도 상당히 만족스럽다고 하시더군요.";
		mes "다음에도 잘 부탁 드려요♥";
		if (getarg(3,0)) delitem getarg(3),getarg(4);
		erasequest getarg(0);
		setquest getarg(0)+10;
		getexp getarg(2),0;
		close;
	}
}
