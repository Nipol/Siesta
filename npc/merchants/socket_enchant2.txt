//===== Hercules Script ======================================
//= Episode 12 Socket Enchant NPC
//===== By: ==================================================
//= Gepard
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//= 1.2 kRO Official Texts & Updated to match the kRO script. [nyalin]
//============================================================

-	script	레아블렘#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[레아블렘]";
	mes "안녕하세요. 저는 방어구에 소켓을 뚫어주는 기술자 레아블렘입니다. 상당히 힘든 일이지만... 나름대로 자부심을 가지고 일을 하고 있지요.";
	next;
	mes "[레아블렘]";
	mes "소켓을 뚫어줬으면 하는 장비가 있으신가요? 장비에 따라서 수수료나 필요재료, 성공확률등이 다르니까... 뭐 일단 골라보시면 제가 말씀해 드릴께요.";
	next;
	switch(select("소켓뚫기 작업:설명을 더 듣는다.:작업방식이 바뀌었네?:관둔다"))
	{
		case 1:
			mes "[레아블렘]";
			mes "소켓뚫기는 작업난이도에 따라 4가지로 분류돼요. 예외적으로 희귀한 SSS급의 장비가 있긴 하지만 흔하게 하는 작업은 아니니 어떤 장비에 소켓뚫기를 하실 건가요? 골라보세요.";
			next;

			//메뉴 생성 착용 장비
			.@menu$ = "";
			for(.@i = 1; .@i <= 10; ++.@i) {
				if(getequipisequiped(.@i)) {
					.@menu$ += .@menu$ + "" + getequipname(.@i) + "";
					.@equipped = 1;
				}
				.@menu$ += ":";
			}
			if (.@equipped == 0) {
				mes "[레아블렘]";
				mes "어떤 장비에 소켓을 뚫어야...";
				close;
			}
			.@part = select(.@menu$);


			// 소켓작업이 가능한 아이템인지 체크 //무기
			if(getequipweaponlv(.@part) > 0 ) {
				mes "[레아블렘]";
				mes "형과 업무협의에 의해 저는 방어구에만 소켓뚫기 작업을 하고 있어요. 무기에 소켓뚫기를 하시려면 세야블렘 형에게 가져가 주세요.";
				close;

			}

			// 소켓작업이 가능한 아이템인지 체크 //방어구
			if(getequipid(.@part) == 2253) callfunc "Func_Socket2",2253,5351,40,66,100,999,3; //Sunflower
			if(getequipid(.@part) == 5012) callfunc "Func_Socket2",5012,5347,40,66,100,999,3; //Ph.D Hat
			if(getequipid(.@part) == 2244) callfunc "Func_Socket2",2244,5348,40,66,100,999,5; //Big Ribbon
			if(getequipid(.@part) == 5016) callfunc "Func_Socket2",5016,5349,40,66,100,999,5; //Boys Cap

			if(getequipid(.@part) == 2609) callfunc "Func_Socket2",2609,2715,40,61,300,999,5;; //Skull Ring
			if(getequipid(.@part) == 2409) callfunc "Func_Socket2",2409,2432,40,61,300,999,5; //High Heels

			if(getequipid(.@part) == 1619) callfunc "Func_Socket2",1619,1620,40,61,500,984,5,999,10; //Survivor's Rod(INT)
			if(getequipid(.@part) == 2287) callfunc "Func_Socket2",2287,5350,50,61,500,985,1; //Pirate Bandana
			if(getequipid(.@part) == 2425) callfunc "Func_Socket2",2425,2434,40,51,500,985,1; //Black Leather Boots

			if(getequipid(.@part) == 1617) callfunc "Func_Socket2",1617,1618,40,51,2000,984,5,999,10; //Survivor's Rod(DEX)
			if(getequipid(.@part) == 2334) callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; //Mage Coat
			if(getequipid(.@part) == 2327) callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; //Holy Robe
			if(getequipid(.@part) == 2111) callfunc "Func_Socket2",2111,2128,45,51,1000,985,1; //Sacred Mission
			if(getequipid(.@part) == 2522) callfunc "Func_Socket2",2522,2523,45,51,1000,985,1; //Undershirt
			if(getequipid(.@part) == 2339) callfunc "Func_Socket2",2339,2371,45,51,1000,985,1; //Pantie

			mes "[세야블렘]";
			mes "죄송한데요. 이 장비에는 소켓뚫기가 힘들 것 같아요. 형이라면 이 장비에 소켓뚫기가 가능할까요? 잘 모르겠네요?";
			close;
		case 2:
			mes "[레아블렘]";
			mes "설명을 더 듣고 싶은신거군요. 음... 뭐 딱히 추가로 설명할 것은 없지만, 한가지 참고할 점이라면 알려드릴 수 있어요.";
			next;
			mes "[레아블렘]";
			mes "그것은 장비의 제련도에 따라 동네마다 소켓 뚫을 확률이 다르다는 점이랄까요. 근거가 있는 이야기는 아닙니다만... 확실히 동네마다 달라요.";
			next;
			mes "[레아블렘]";
			mes "예를 들어서 어떤 마을은 제련도가 높은 장비가 더 소켓이 잘 뚫리구요. 어떤 마을은 제련도가 낮은 장비가 더 잘 뚤리고... 그래요.";
			next;
			mes "[레아블렘]";
			mes "소켓을 뚫는데 실패하면 장비가 산산조각이 나서 어찌 할 수가 없어요. 최대한 성공시키려고 노력은 합니다만... 워낙 섬세한 작업인지라.";
			next;
			mes "[레아블렘]";
			mes "그리고 주의하실 점이 하나더 있는데.. +7 망토를 제게 소켓 뚫는 의뢰를 하시면, 장비는 어떻게 될 거 같으세요?";
			next;
			mes "[레아블렘]";
			mes "정답은 소켓뚫기가 성공하던 실패하던 부탁한 장비의 제련수치는 모두 사라지게 돼요. 괜히 잘못 작업을 시도했다가 실패하면 날리기만 할 뿐이니까요.";
			next;
			mes "[레아블렘]";
			mes "아무튼 확실히 체크해주시고, 소켓뚫기 작업을 의뢰해주시기 바래요. 부탁 드리겠습니다.";
			close;
		case 3:
			mes "[레아블렘]";
			mes "예전부터 손님들이 불편하다고 요청하던 것들이 있어서요. 이번에 형과 작업할 것을 분리하면서 작업방식도 변경하게 되었어요.";
			next;
			mes "[레아블렘]";
			mes "형은 무기를 전담하게 되었구요. 전 형이 하던 방어구까지 받아서 전담하게 되었어요.";
			next;
			mes "[레아블렘]";
			mes "헤헤 형이 이제 제가 방어구는 더 잘하는 것 같다나 뭐라나. 이정도면 형에게 인정받았다고 봐도 되겠죠?";
		case 4:
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	레아블렘#pay	4_M_04
lighthalzen,96,137,3	duplicate(SocketEnchant2)	레아블렘#lhz	4_M_04
prt_in,22,60,6	duplicate(SocketEnchant2)	레아블렘#prt	4_M_04
moc_ruins,154,86,3	duplicate(SocketEnchant2)	레아블렘#moc	4_M_04

function	script	Func_Socket2	{

	disable_items;
	mes "[레아블렘]";
	if(getarg(7,0) != 0 && getarg(8,0) != 0)
	{
		mes "" + getitemname(getarg(0)) + "인가요. 오케이. 그거라면";
		mes "요금은 ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 제니^000000 따로 준비해 와야 하는 재료는 ^FF0000" + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + " " + getarg(6) + " 개^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "s":"") + " " + getarg(8,0) + "개^000000") : "") + "이야.";
		mes "물론 " + getitemname(getarg(0)) + " 당연히 가져와야돼요.";
	}
	else
	{
		mes "" + getitemname(getarg(0)) + "인가요. 오케이. 그거라면";
		mes "요금은 ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 제니^000000 따로 준비해 와야 하는 재료는 ^FF0000" + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + " " + getarg(6) + " 개^000000이야.";
		mes "물론 " + getitemname(getarg(0)) + " 당연히 가져와야돼요.";
	}
	next;
	mes "^FF0000주의 - 인벤토리에 있는 " + getitemname(getarg(0)) + "이 사용될수도 있음.^000000";
	mes "재료랑 요금은 다 챙겨왔어요? 그리고, 실패할 경우 재료.장비는 다 없어져 버리니 그 점은 인지해주세요, 소켓뚫기에 성공해도 제련치나 박아둔 카드는 전부 사라진다는 것도 명심하세요.";
	next;
	switch(select("소켓뚫기 작업을 맡긴다.:다음 기회에 도전한다."))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[레아블렘]";
				mes "좋아. 이제 시작할게요!";
				mes "성공하기를 기도하는게 좋을겁니다.";
				next;
				.@a = rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[레아블렘]";
					mes "좋아요! 성공이에요!";
					mes "예쁘게 잘 만들어진거같아요. 축하드려요!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					Zeny -= getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[레아블렘]";
					mes "다시만나요, 손님!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[레아블렘]";
					mes "헉! 정말 미안해요.. 실패해버렸네요.";
					mes "하지만 이건 당신의 운이에요. 신께서 당신에게 정해주신 운명이라고요. 알았어요?";
					mes "너무 실망하지 마세요. 다음 기회가 있어요.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					Zeny -= getarg(4)*1000;
					next;
					mes "[레아블렘]";
					mes "다음번에는 꼭 성공하길 바래요!";
					close;
				}
			}
			else
			{
				mes "[레아블렘]";
				mes "바보에요? 재료가 부족하잖아요! 다시 제대로 가져오세요!";
				close;
			}
		case 2:
			mes "[레아블렘]";
			mes "그럼 다음 기회에 또.";
			close;
	}
}
