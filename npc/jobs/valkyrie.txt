//===== Hercules Script ======================================
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//= L0ne_W0lf
//===== Current Version: =====================================
//= 3.2
//===== Description: =========================================
//= [Official Conversion]
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//===== Additional Comments: =================================
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=     Removed all functions as they are no longer required.
//=     Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=     scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//= 3.0 Added quest log commands. [L0ne_W0lf]
//= 3.1 Added Izlude Renewal coordinates.
//= 3.2 Added "도망친 책" Quest. [nyalin]
//============================================================

valkyrie,48,86,4	script	Valkyrie#	4_F_VALKYRIE,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Please make";
		mes "yourself comfortable";
		mes "while you are here.";
		mes "Honor to the warriors!";
		close;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "You will now end";
		mes "your present life and";
		mes "begin an entirely new life.";
		mes "Honor to the warriors!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() || checkfalcon() || checkmount()) {
			mes "[Valkyrie]";
			mes "There are a few things you must";
			mes "do before we start. You must";
			mes "first empty your mind and body.";
			mes "Honor comes when you abandon";
			mes "all your selfish desires...";
			next;
			mes "[Valkyrie]";
			mes "You cannot take anything with you to the next life. Your items, zeny, pets and Pushcart all have to be left behind.";
			next;
			mes "[Valkyrie]";
			mes "When you are ready";
			mes "please return to me,";
			mes "brave adventurer.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "I see you've already";
			mes "released yourself from";
			mes "all worldy attachments,";
			mes ""+ strcharinfo(0) +".";
			next;
			mes "[Valkyrie]";
			mes "That's an admirable attitude for an adventurer such as yourself. Honor comes when you abandon all personal desires for the sake of mankind.";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "Hmm... I sense that you have";
				mes "some lingering attachment or";
				mes "unfinished business in your";
				mes "current life. Take care of that,";
				mes "and bring closure to your present life.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "Now, let me remove all";
			mes "of your present memories...";
			mes "However, you will be able to";
			mes "remember the most honorable";
			mes "moments of this life.";
			next;
			mes "[Valkyrie]";
			mes "With one,";
			mes "I will ask the";
			mes "goddess Urd to remove";
			mes "all of your present";
			mes "memories.";
			next;
			mes "[Valkyrie]";
			mes "With two,";
			mes "I will ask the";
			mes "goddess Verdandi to keep";
			mes "and record the most honorable moments of your present life.";
			next;
			mes "[Valkyrie]";
			mes "With three,";
			mes "I will ask the";
			mes "goddess Skuld to";
			mes "guide you to your";
			mes "next life.";
			next;
			mes "[Valkyrie]";
			mes "One...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "One...";
			mes "Two......";
			next;
			mes "[Valkyrie]";
			mes "One...";
			mes "Two......";
			mes "And Three.";
			ADVJOB = Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) ADVJOB = Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) ADVJOB = Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			MISC_QUEST |= 1024; //<-reset Skill Reset Event
			skill 142,1,0;
			skill 143,1,0;
			completequest 1000;
			next;
			mes "[Valkyrie]";
			mes "Congratulations.";
			mes "You are now reborn";
			mes "into a brand new life.";
			mes "Please take these small gifts";
			mes "in preparation for your new adventures.";
			getitem 1202,1; //Knife_
			getitem 2302,1; //Cotton_Shirt_
			next;
			mes "[Valkyrie]";
			mes "I wish that the release the goddess Urd has granted you proves to be a blessing. I hope that the memories Verdandi has recorded will always honor you.";
			next;
			mes "[Valkyrie]";
			mes "And I pray that the new life to which the goddess Skuld will guide you will be even more honorable than your last.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (RENEWAL) warp "izlude",129,97;
				else warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Unfortunately, you have not yet been invited here. I ask you to leave immediately. Honor to the warriors!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	메테우스 실프#Library	2_M_SAGE_B,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (questprogress(5160) < 2) {
			if (countitem(6586) > 0) .@Book = 1;
			mes "[메테우스 실프]";
			mes "어서 오세요. 슈바이체르빌 마법학교 도서관입니다. 도서관가드들이 보내온 쪽지를 확인하러 오셨나요?";
			next;
			switch(select(""+(.@Book?"책을 찾아왔습니다.":"수색상황을 알려주세요.")+":제니를 기부하겠습니다.")) {
			 case 1:
				if (.@Book == 1) {
					mes "[메테우스 실프]";
					mes "좀 전에 도서관가드들이 허탕치고 돌아왔다고 보고를 했었는데 다 이유가 있었군요.";
					next;
					mes "[메테우스 실프]";
					mes "뭐 중요한 건 도망친 책을 되찾아왔다는 거겠죠?";
					next;
					mes "[메테우스 실프]";
					mes "약속했던 대로 이미르의 서 열람을 할 수 있게 해드렸습니다. 들어가셔서 열람하셔도 좋습니다.";
					valkyrie_Q = 1;
					delitem 6586,1; //Caught_Book
					erasequest 5160;
					setquest 1000; //when talk with valkyrie erase quest.
					close;
				} else {
					mes "[메테우스 실프]";
					mes "조금 전 수색 중이던 도서관 가드들이 보내온 쪽지 입니다.";
					next;
					mes "[도서관 가드의 쪽지내용]";
					mes "유노인근 움직이는 책 목격제보 수색 중";
					next;
					mes "[메테우스 실프]";
					mes "그렇게 자세한 위치설명은 나와있지 않군요. 그나마 수색범위가 줄은 것이 다행이군요.";
					next;
					mes "[메테우스 실프]";
					mes "수색위치가 막막하다면,";
					mes "<킬 하이르 학원>지역";
					mes "먼저 찾아보는 게 어떻겠습니까?";
					next;
					mes "[메테우스 실프]";
					mes "책이 한 권만 탈출한 건 아니니 말입니다.";
					close;
				}
			case 2:
				if (Zeny >= 1285000) {
					Zeny -= 1285000;
					valkyrie_Q = 1;
					mes "[메테우스 실프]";
					mes "Thank you, your donation will be used for a good cause. You may";
					mes "now go in and read the book.";
					delitem 6586,1; //Caught_Book
					erasequest 5160;
					close;
				} else {
					mes "[메테우스 실프]";
					mes "Unfortunately, you don't seem to possess 1,285,000 zeny at the moment. Please check your funds and come back again.";
					close;
				}
			}
		}
		if (valkyrie_Q == 0) {
			mes "[메테우스 실프]";
			mes "Welcome to the Library of the Schweicherbil Magic Academy.";
			mes "I assume you have come here";
			mes "to read the 'Book of Ymir.'";
			next;
			mes "[메테우스 실프]";
			mes "Unfortunately, the original copy of the book has been damaged over time. We currently only allow the public to view a copy of the book.";
			next;
			mes "[메테우스 실프]";
			mes "Also, in order to preserve the original 'Book of Ymir,' we have decided to accept donations from people who wish to read the copy we have provided.";
			next;
			mes "[메테우스 실프]";
			mes "The suggested";
			mes "donation amount is";
			mes "1,285,000 zeny.";
			next;
			switch(select("Donate.:그냥 보면 안될까요?:Cancel.")) {
			case 1:
				if (Zeny >= 1285000) {
					Zeny -= 1285000;
					valkyrie_Q = 1;
					mes "[메테우스 실프]";
					mes "Thank you, your donation will be used for a good cause. You may";
					mes "now go in and read the book.";
					close;
				} else {
					mes "[메테우스 실프]";
					mes "Unfortunately, you don't seem to possess enough zeny at the moment. Please check your funds and come back again.";
					close;
				}
			case 2:
				mes "[메테우스 실프]";
				mes "서적의 안전한 보존과 다른 고서적 연구를 위한 기금이라고 말씀 드린 것 같은데요?";
				next;
				mes "[메테우스 실프]";
				mes "보고 싶으신 마음은 이해하지만 안될 것 같습니다.";
				next;
				select("하지만 그렇게 큰 돈은 없습니다.");
				mes "[메테우스 실프]";
				mes "당당하신 분이군요. 원칙대로라면 도서관에 기부금으로 공헌을 한 분께만 공개를 하는 것이 원칙이긴 한데..";
				next;
				mes "[메테우스 실프]";
				mes "사람이 살아가면서 항상 원칙대로만 살수는 없을 겁니다.";
				next;
				switch(select("사람이 원칙대로 살아야죠.","뭐든지 하겠습니다.")) {
				case 1:
					mes "[메테우스 실프]";
					mes "사람을 무안하게 만드시는 재주가 있군요. 그 말 그대로 원칙대로 사시기 바랍니다.";
					close;
				case 2:
					mes "[메테우스 실프]";
					mes "열의가 보이는 군요. 그래도 그냥 보여드리기는 곤란하고";
					next;
					mes "[메테우스 실프]";
					mes "슈바이체르빌 마법학교 도서관에 발생한 문제를 도와주시면, 이미르의 서를 열람할 수 있게 도와드리겠습니다.";
					while(1) {
						next;
						switch(select("발생한 문제가 무엇입니까?:탈출한 책은 어디에?:저도 찾아보겠습니다.:무시한다.")) {
						case 1:
							mes "[메테우스 실프]";
							mes "골치 아픈 문제입니다.";
							mes "오랜 역사를 자랑하는 슈바이체르빌 마법학교 도서관에는 일반에 공개되지 않은 책들이 존재합니다.";
							next;
							mes "[메테우스 실프]";
							mes "^0000FF라이드워드^000000같은 사람을 공격하는 살아있는 책들이 비공개인데 얼마 전 봉인된 책들의 일부가";
							next;
							mes "[메테우스 실프]";
							mes "^0000FF봉인된 책장은 지긋지긋해!!^000000";
							mes "라고 외치며 도서관을 탈출하는 사고가 있었습니다.";
							next;
							mes "[메테우스 실프]";
							mes "도서관가드들이 탈출한 책을 찾고는 있지만, 인원부족으로 어려움을 겪고 있습니다.";
							break;
						case 2:
							mes "[메테우스 실프]";
							mes "저희도 아직 파악 중입니다. 도서관가드들께 새 정보가 들어오는 대로 알려 드리도록 하겠습니다.";
							next;
							break;
						case 3:
							mes "[메테우스 실프]";
							mes "아니 문제를 벌써 알고 계신 겁니까? 이상하군요. 혹시 다른 걸 알고 계신 건 아닙니까?";
							while(1) {
								next;
								switch(select("죄송합니다.:여자친구선물고민 아닌가요?:오늘 점심이 고민 아닌가요?:탈출한 책이 고민 아닌가요?")) {
								case 1:
									mes "[메테우스 실프]";
									mes "역시 그럴 것 같았습니다. 우선 문제가 무엇인지 알아야 할 것 같군요.";
									break;
								case 2:
									mes "[메테우스 실프]";
									mes "여차친구가 있는지 먼저 물어보는 게 예의 아닙니까? 그리고 이건 문제가 아닙니다.";
									break;
								case 3:
									mes "[메테우스 실프]";
									mes "요즘 점심을 안 먹고 있으니 문제가 아닙니다. 제 문제 말고 도서관 문제를 들으셔야겠군요.";
									break;
								case 4:
									mes "[메테우스 실프]";
									mes "앗! 알고 계셨나요? 이런 벌써 외부에 까지 소문이 퍼진 모양이군요.";
									next;
									mes "[메테우스 실프]";
									mes "도서관 가드에게 현재 수색상황이 쪽지로 들어 왔습니다.";
									next;
									setquest 5160;
									mes "[메테우스 실프]";
									mes "바로 떠나지 마시고 현재 수색상황을 듣고 가시기 바랍니다.";
									close;
								}
							}
						case 4:
							mes "[메테우스 실프]";
							mes "이상한 분이시군요.";
							close;
						}
					}
				}
			case 3:
				mes "[메테우스 실프]";
				mes "Take your time, and";
				mes "enjoy your travels.";
				close;
			}
		}
		mes "[메테우스 실프]";
		mes "Once again, thank you for your generous donation. Feel free to read a carbon copy of the 'Book of Ymir' at your leisure.";
		close;
	}
	mes "[메테우스 실프]";
	mes "Welcome to the Library of the Schweicherbil Magic Academy.";
	mes "Here, we have a countless number of books. Please take your time and look around.";
	close;
}

yuno_in02,93,207,1	script	Book of Ymir	HIDDEN_NPC,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[The Book of Ymir]";
		mes "...The entrance to the Hall of Honor is open to anyone who has moved forward into their next life. It is there to help heroes decide what they want to do, and can lead them to anywhere in this world.";
		next;
		mes "[The Book of Ymir]";
		mes "In the Hall of Honor, everything is prepared for heroes. It is rumored that any wish that cannot be fulfilled in our reality can be realized in the Hall of Honor.";
		next;
		if (select("Stop reading.:Continue reading.") == 1) {
			mes "[The Book of Ymir]";
			mes ".....";
			close;
		}
		mes "[The Book of Ymir]";
		mes "There is a forgotten path which leads to the Hall of Honor, the closest place to the heavens. The ordinary will never discover this place...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[The Book of Ymir]";
			mes "...Therefore, ancient heroes were";
			mes "always in anguish, knowing that";
			mes "eventually, they were mortal and";
			mes "would pass from this realm...";
			next;
			mes "[The Book of Ymir]";
			mes "There were no documents,";
			mes "songs, or remaining folklore that had any information on life after death. However, I recently uncovered an old scroll";
			mes "about Valkyrie...";
			next;
			mes "[The Book of Ymir]";
			mes "Valkyrie...";
			mes "The legendary";
			mes "guardian angel.";
			mes "Angel of Ragnarok.";
			next;
			mes "[The Book of Ymir]";
			mes "Adventurers of great strength";
			mes "and bravery will be lead by";
			mes "Valkyrie to Valhalla, the Hall";
			mes "of Honor. There, they will be";
			mes "given a new life.";
			next;
			mes "[The Book of Ymir]";
			mes "Reborn, they will live again as";
			mes "even greater heroes that will";
			mes "brighten the world. Bodies that";
			mes "were exhausted will be filled";
			mes "with energy...";
			next;
			mes "[The Book of Ymir]";
			mes "And their souls will be given abilities with the heart of Ymir. However, the heart of Ymir was totally destroyed and scattered all over the world after the battle for Rune-Midgarts.";
			next;
			mes "[The Book of Ymir]";
			mes "I have found a small amount of";
			mes "Ymir heart pieces over a long";
			mes "long period of time. But I can't";
			mes "confirm if the story of Valkyrie";
			mes "and Valhalla is true just";
			mes "through scientific tests.";
			next;
			mes "[The Book of Ymir]";
			mes "So, I am leaving this record in hope that someone in the future";
			mes "can confirm that Valkyrie and Valhalla actually exist...";
			next;
			valkyrie_Q = 2;
			if(!questprogress(1000)) {
				setquest 1000;
			}
			mes "[The Book of Ymir]";
			mes "Let the heroes live new lives";
			mes "so they can protect the world";
			mes "from danger. And then...";
			close;
		}
		mes "[The Book of Ymir]";
		mes "...";
		close;
	}
	mes "[The Book of Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Heart of Ymir	HIDDEN_NPC,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporter	4_F_TELEPORTER,{
	if (Upper != 1) {
		.@karma_d = rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleporter]";
			mes "Congratulations.";
			mes "Honor to the warriors!";
			close;
		}
		mes "[Teleporter]";
		mes "Please refrain";
		mes "from touching any";
		mes "of the exhibitions.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleporter]";
		mes "Honorable one,";
		mes "which place do you wish to go?";
		next;
		switch(select("Prontera:Morroc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (RENEWAL) callsub S_Warpchar,"izlude",129,97;
			else callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_Warpchar:
	mes "[Teleporter]";
	mes "Have a nice trip.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2);
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

// Monsters
yuno_fild02,0,0,0	script	bookquest	-1,{
	end;
OnInit:
	enablenpc "bookquest";
	donpcevent "bookquest::OnEnable";
	end;
OnEnable:
	monster "yuno_fild01",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild02",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild03",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild04",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild05",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild06",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild07",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	monster "yuno_fild08",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead";
	end;
OnMyMobDead:
	if (questprogress(5160) < 2 && countitem(6586) == 0) getitem 6586,1; //Caught_Book
	sleep2 5000;
	switch(atoi(replacestr(strnpcinfo(2),"yuno_fild",""))) {
		case 1: monster "yuno_fild01",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 2: monster "yuno_fild02",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 3: monster "yuno_fild03",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 4: monster "yuno_fild04",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 5: monster "yuno_fild05",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 6: monster "yuno_fild06",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 7: monster "yuno_fild07",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
		case 8: monster "yuno_fild08",0,0,"도망친 책",2414,1,"bookquest::OnMyMobDead"; break;
	}
	end;
}