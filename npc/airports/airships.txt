//===== Hercules Script ======================================
//= The Airship System Script
//===== By: ==================================================
//= Hercules Dev Team
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= The Airship System used in the official servers.
//===== Additional Comments: =================================
//= Official NPCs.
//= 1.1 kRO Official Texts. [nyalin]
//============================================================

//============================================================
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================

airplane,243,73,0	script	#AirshipWarp-1	WARPNPC,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	WARPNPC,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","현재 이 비공정은 아인브로크를 향해 날아가고 있습니다.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","잠시 후 이 비공정은 아인브로크에 착륙합니다.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation = 1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","아인브로크에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","현재 아인브로크에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","비공정 이륙하겠습니다. 다음 목적지는 리히타르젠입니다.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","현재 이 비공정은 리히타르젠을 향해 날아가고 있습니다.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","잠시 후 이 비공정은 리히타르젠에 착륙합니다.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation = 2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","리히타르젠에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","현재 리히타르젠에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","비공정 이륙하겠습니다. 다음 목적지는 아인브로크입니다.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","현재 이 비공정은 아인브로크를 향해 날아가고 있습니다.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","잠시 후 이 비공정은 아인브로크에 착륙합니다.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	$@airplanelocation = 1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","아인브로크에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","현재 아인브로크에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","비공정 이륙하겠습니다. 다음 목적지는 유노입니다.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","현재 이 비공정은 유노를 향해 날아가고 있습니다.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","잠시 후 이 비공정은 유노에 착륙합니다.",bc_map,"0xff8200";
	end;
OnTimer300000:
	$@airplanelocation = 0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","유노에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","현재 유노에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","비공정 이륙하겠습니다. 다음 목적지는 휘겔입니다.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","현재 이 비공정은 휘겔을 향해 날아가고 있습니다.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","잠시 후 이 비공정은 휘겔에 착륙합니다.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	$@airplanelocation = 3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","휘겔에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","현재 휘겔에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","비공정 이륙하겠습니다. 다음 목적지는 유노입니다.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","현재 이 비공정은 유노를 향해 날아가고 있습니다.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","잠시 후 이 비공정은 유노에 착륙합니다.",bc_map,"0xff8200";
	end;
OnTimer460000:
	$@airplanelocation = 0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","유노에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","현재 유노에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","비공정 이륙하겠습니다. 다음 목적지는 아인브로크입니다.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	출 구#airplane1a::ExitAirplane	4_BOARD3,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	출 구#airplane1b	4_BOARD3
airplane,240,40,1	duplicate(ExitAirplane)	출 구#airplane2a	4_BOARD3
airplane,247,40,1	duplicate(ExitAirplane)	출 구#airplane2b	4_BOARD3

airplane,100,69,3	script	승무원#ein-1	4_M_EIN_SOLDIER,{
	mes "[승무원]";
	mes "비공정에서 내리시려면 위쪽으로 가는 계단을 이용해 주십시오.";
	mes "비공정을 이용해주셔서 감사합니다. 좋은 하루 되시길 바랍니다.";
	close;
}

airplane,64,94,1	script	움발라 꼬마#ein_p	4_M_UMKID,{
	emotion e_swt2;
	mes "[꼬마]";
	if (event_umbala >= 3) {
		mes "와아-!, 엄마!";
		mes "이..이것 보세요.";
		mes "우리가 날고 있어요!";
		mes "우리가 이 멋진 하늘을 날고 있어요!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	움발라 부인#ein_p	4_F_UMWOMAN,{
	emotion e_dots;
	mes "[부인]";
	if (event_umbala >= 3) {
		mes "쉬-잇...";
		mes "얘야, 가만히 있으렴~";
		mes "공공장소에서 너무 떠들면 안된단다.";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	움발라 남편#ein_p	4_M_UMSOLDIER,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Hey there~";
		mes "From that look on";
		mes "your face, I see that";
		mes "you can understand";
		mes "me. ^333333*Whew...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "I've been helping the";
		mes "Airship enterprise by";
		mes "having the Airship Crewmen";
		mes "train in Umbala to overcome";
		mes "any acrophobia they might have through bungee jumping. Neat, eh?";
		next;
		mes "[Chrmlim]";
		mes "But...";
		mes "Some of them couldn't";
		mes "overcome their fear of";
		mes "heights. And a few even";
		mes "ended up, um, ^333333in Nifflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	비공정 안내직원#air	1_F_02,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[비공정 안내직원]";
		mes "Welcome";
		mes "to the Airship.";
		mes "How may I help you?";
		next;
		select("Do you have a passenger named Thierry?");
		mes "[비공정 안내직원]";
		mes "I am sorry, but I do not think that we have a passenger by that name.";
		close;
	}
	// Hugel quest end
	mes "[비공정 안내직원]";
	mes "안녕하세요. 저희 비공정에선 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 무엇을 도와드릴까요?";
	next;
	switch (select("비공정을 이용하는 방법:선장실의 위치:내부 시설 안내:취소")) {
	case 1:
		mes "[비공정 안내직원]";
		mes "목적지까지 비공정을 통해 가시려면 안내방송에서 목적지의 도착을 알렸을때, 양쪽에 있는 출구를 통해 나가시면 됩니다.";
		next;
		mes "[비공정 안내직원]";
		mes "만약 내리실 타이밍을 놓치셨더라도, 비공정은 계속 순환하여 운행하니 조금만 기다리셨다가 다음 도착 때 내리세요.";
		close;
	case 2:
		mes "[비공정 안내직원]";
		mes "선장실은 비공정의 맨 앞에 있습니다. 그 안에는 비공정을 지휘하시는 선장님과 배를 조종하는 조타수가 비공정을 운전하고 있습니다. 선장님이 정말 좋은 분이시니 꼭 한 번 만나보세요.";
		close;
	case 3:
		mes "[비공정 안내직원]";
		mes "비공정 내부엔 고객님들이 여행하시는 동안 지루하지 않도록 작은 미니게임들이 마련돼 있습니다. 고단한 여행에 심신이 지치셨을때 잠시 쉴 수 있는 공간이니 많이 이용해주세요.";
		close;
	case 4:
		mes "[비공정 안내직원]";
		mes "편안한 여행 되시길 바랍니다.";
		close;
	}
}

airplane,80,71,2	script	제르타#01airplane	4_M_BUDDHIST,{
	mes "[제르타]";
	mes "가르침을 전파하기 위해 여행길에 올랐소. 이 가르침으로 룬 미드가츠 전체를 단결시키는게 나의 소망이라오.";
	close;
}

airplane,65,63,4	script	소 녀#01airplane	4_F_06,{
	mes "[소 녀]";
	mes "이 비공정 루티에 가죠? 근데 암만 기다려도 루티에에 도착했다는 방송이 안 뜨네...";
	close;
}

airplane,72,34,6	script	아노스#01airplane	4_F_GON,{
	mes "[아노스]";
	mes "하늘에서 바라본 경치는 정말 좋네요. 새가 되어 훨훨 날아다닌다면 얼마나 좋을까.";
	close;
}

airplane,221,158,2	script	Pilot#airplane	4_M_EIN_SOLDIER,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "I wish that I could go drink a cold fresh beer.";
		mes "Drinking is the goal of my life! Drinking gives me energy!";
		mes "I am nothing without drinks!";
		next;
		mes "[Pilot]";
		mes "But! Driving under the influence is not good.";
		mes "But! That makes me want to drink more and more!";
		emotion e_sob;
		next;
		select("Do you know a passenger named Thierry?");
		mes "[Pilot]";
		mes "This uniform is";
		mes "really dapper, but";
		mes "it's way too thick to";
		mes "wear around the Airship.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "No one ever really";
		mes "comes into this room.";
		mes "And the captain IS a reindeer. I could just strip to my boxers.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Wah!? Who is it!";
		next;
		mes "- ...He is not listening to you, at all. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "It's been sooo";
		mes "long since I've";
		mes "enjoyed a nice, cold";
		mes "alcoholic brew. But the";
		mes "job requires me to be as";
		mes "clear headed as I can!";
		next;
		mes "[Pilot]";
		mes "Always drink responsibly!";
		mes "Still, I can't remember the";
		mes "last time I had a real vacation";
		mes "or even a day off. Yeap, some";
		mes "booze, some chips, some TV";
		mes "and serius R&R is in order.";
		emotion e_sob;
		close;
	case 2:
		mes "[Pilot]";
		mes "Man, the weather";
		mes "is really nice today.";
		mes "Bright, open skies make";
		mes "for some good visibility";
		mes "and safe, carefree flying.";
		close;
	case 3:
		mes "[Pilot]";
		mes "You know, our captain's a";
		mes "respectable guy. Him and";
		mes "his brother are actually well";
		mes "known in the aircraft industry.";
		mes "Who knew reindeer made";
		mes "such good captains?";
		next;
		mes "[Pilot]";
		mes "Just between you";
		mes "and me, I gotta tell";
		mes "you, that Santa was onto";
		mes "something, getting reindeers";
		mes "and elves to work for him.";
		mes "The man must be a genius!";
		close;
	default:
		mes "[Pilot]";
		mes "You know, this whole";
		mes "piloting thing in the air,";
		mes "it's rather new, you know?";
		mes "Yeah, they got this Airship";
		mes "operation in a hurry.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Still, they where real";
		mes "serius, really thought";
		mes "ahead. I mean, they had us";
		mes "training while the Airships";
		mes "were still being invented.";
		mes "Isn't that freakin' crazy?!";
		close;
	}
}

airplane,50,66,5	script	사과상인#airplane	4_M_04,{
	mes "[훌츠]";
	mes "어서오세요. 싱싱한 사과를 팔거나, 사과를 사과 쥬스로 만들어 드립니다~.";
	next;
	switch (select("사과를 구입한다.:사과를 사과 쥬스로 만든다.:여기에 웬 사과상인이?:취소")) {
	case 1:
		mes "[훌츠]";
		mes "구입하실 양 만큼 숫자를 입력하세요.";
		mes "사과는 개당 15Z로 한번에 500개까지 구입 가능합니다. 구입을 취소 하시려면 0을 입력해 주세요.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[훌츠]";
				mes "다음에 또 이용해 주세요. 그럼 안녕히~.";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[훌츠]";
				mes "입력 값을 초과하셨네요. 다시 입력해 주세요.";
				next;
			}
			else {
				mes "[훌츠]";
				mes "구입하시려는 사과의 갯수는 ^FF0000" + .@input + "개^000000로";
				mes "지불하셔야 할 요금은 ^FF0000" + .@pay + " Zeny^000000 입니다.";
				mes "구입하시겠습니까?";
				next;
				if (select("예:아니오") == 2) {
					mes "[훌츠]";
					mes "다음에 또 이용해 주세요. 그럼 안녕히~.";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[훌츠]";
			mes "갖고 계신 돈이 모자라신 것 같네요. 돈을 더 갖고 오시거나, 수를 좀 줄여서 다시 구입해 주세요.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[훌츠]";
			mes "흠.. 너무 많이 들고 계신거 같아요. 가방에 다 안들어갈 것 같아요.";
			close;
		}
		else {
			Zeny -= .@pay;
			getitem 512,.@input; //Apple
			mes "[훌츠]";
			mes "감사합니다. 사과가 필요하시면 언제든지 오세요.";
			close;
		}
	case 2:
		mes "[훌츠]";
		mes "사과 쥬스는 ^FF0000사과 3개, 빈 병 1개^000000를 저에게 주시면 만들어 드립니다. 사과 쥬스를 만들어 드릴까요?";
		next;
		switch (select("예:아니오")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[훌츠]";
				mes "갖고 계신 재료가 부족한 거 같네요. 다시 알려드릴게요. ^FF0000사과 3개, 빈 병 1개^000000를 저에게 주시면 만들어 드립니다. 자료가 준비 된다면 절 찾아주세요.";
				close;
			}
			else {
				mes "[훌츠]";
				mes "네. 재료 확인했습니다. 잠시만 기다려주세요~.";
				next;
				mes "^3355FF*스으윽삭*";
				mes "*지이이이잉~*";
				mes "*우지끈 뚝딱...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[훌츠]";
				mes "여기 있습니다. 다음에도 필요하면 절 찾아주세요. 그럼 안녕히~.";
				close;
			}
		case 2:
			mes "[훌츠]";
			mes "다음에 또 이용해 주세요. 그럼 안녕히~.";
			close;
		}
	case 3:
		mes "[훌츠]";
		mes "전 원래 이곳 저곳을 떠돌아 다니며 사람들에게 밥을 구걸하는 거지였습니다. 그러다가 어느 날 나무아래에서 곤히 잠을 자고 있는데, 무언가가 제 머리 위로 쿵 하고 떨어지는 게 아니겠어요.";
		next;
		mes "[훌츠]";
		mes "깜짝 놀라 잠에서 깨어나보니 제 잠을 깨운 건 다름아닌 사과였습니다. 제가 자고 있던 곳의 나무가 사과나무였던 거죠.";
		next;
		mes "[훌츠]";
		mes "마침 배가 고파서 사과를 먹으려 했는데, 비공정의 히메르 아저씨가 급히 좀 도와달라며 절 비공정으로 부르는 거예요. 잠시 도와드린 뒤, 비공정을 둘러봤는데 사과를 걸고 하는 주사위 게임이 있더라구요.";
		next;
		mes "[훌츠]";
		mes "그래서 심심하기도 했고, 혹시나 싶은 마음에 아까 떨어졌던 사과를 걸고 게임을 해봤죠. 그런데 그 날 운이 좀 좋았는지, 1개였던 사과가 2개가 되고, 2개였던 게 4개가 되기 시작하는 거에요.";
		next;
		mes "[훌츠]";
		mes "그러다가 이렇게 사과상인이 될 정도로 사과를 얻었죠. 좀 황당한가요? 그 날 떨어진 사과 하나가 저의 운명을 이리 바꿔놓은 겁니다. 이야기가 좀 길어졌네요.";
		next;
		mes "[훌츠]";
		mes "건강에 좋으니 사과 많이 드세요. 싫어하고 좋아하고는 상관 없이, 빨간 사과를 먹는 겁니다. 무서운 저승사자도 사과를 준다고 하면 죽는 시늉을 한다고 할 정도로 사과는 좋은 과일이죠.";
		close;
	case 4:
		mes "[훌츠]";
		mes "다음에 또 이용해 주세요. 그럼 안녕히~.";
		close;
	}
}

//============================================================
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================

airplane_01,243,73,0	script	#AirshipWarp-3	WARPNPC,1,1,{
	end;
	
OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1:
		if (RENEWAL)

			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	WARPNPC,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","현재 이 비공정은 이즈루드를 향해 날아가고 있습니다.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","잠시 후 이 비공정은 이즈루드에 착륙합니다.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation2 = 1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","이즈루드에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","현재 이즈루드에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","비공정 이륙하겠습니다. 다음 목적지는 유노입니다.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","현재 이 비공정은 유노를 향해 날아가고 있습니다.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","잠시 후 이 비공정은 유노에 착륙합니다.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation2 = 2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","유노에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","현재 유노에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","비공정 이륙하겠습니다. 다음 목적지는 라헬입니다.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","현재 이 비공정은 라헬을 향해 날아가고 있습니다.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","잠시 후 이 비공정은 라헬에 착륙합니다.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	$@airplanelocation2 = 0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","라헬에 도착했습니다. 내리실 분 안녕히 가세요.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","현재 라헬에 정차 중입니다. 잠시 후 출발하겠습니다.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","비공정 이륙하겠습니다. 다음 목적지는 이즈루드입니다.",bc_map,"0x00ff00";
	stopnpctimer;
	++.moninv;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		.moninv = 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	출 구#airplane_011a::ExitAirplane01	4_BOARD3,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	출 구#airplane_011b	4_BOARD3
airplane_01,240,40,1	duplicate(ExitAirplane01)	출 구#airplane_012a	4_BOARD3
airplane_01,247,40,1	duplicate(ExitAirplane01)	출 구#airplane_012b	4_BOARD3

airplane_01,250,58,2	script	비공정 안내직원#air01	1_F_02,{
	mes "[비공정 안내직원]";
	mes "안녕하세요. 저희 비공정에선 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 무엇을 도와드릴까요?";
	next;
	switch (select("비공정을 이용하는 방법:선장실의 위치:내부 시설 안내:취소")) {
	case 1:
		mes "[비공정 안내직원]";
		mes "목적지까지 비공정을 통해 가시려면 안내방송에서 목적지의 도착을 알렸을때, 양쪽에 있는 출구를 통해 나가시면 됩니다.";
		next;
		mes "[비공정 안내직원]";
		mes "만약 내리실 타이밍을 놓치셨더라도, 비공정은 계속 순환하여 운행하니 조금만 기다리셨다가 다음 도착 때 내리세요.";
		close;
	case 2:
		mes "[비공정 안내직원]";
		mes "선장실은 비공정의 맨 앞에 있습니다. 그 안에는 비공정을 지휘하시는 선장님과 배를 조종하는 조타수가 비공정을 운전하고 있습니다. 선장님이 정말 좋은 분이시니 꼭 한 번 만나보세요.";
		close;
	case 3:
		mes "[비공정 안내직원]";
		mes "비공정 내부엔 고객님들이 여행하시는 동안 지루하지 않도록 작은 미니게임들이 마련돼 있습니다. 고단한 여행에 심신이 지치셨을때 잠시 쉴 수 있는 공간이니 많이 이용해주세요.";
		close;
	case 4:
		mes "[비공정 안내직원]";
		mes "편안한 여행 되시길 바랍니다.";
		close;
	}
}

airplane_01,50,66,5	script	사과상인#air01	4_M_04,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you loose some weight. -";
		close;
	}
	mes "[Meltz]";
	mes "Welcome to Meltz's";
	mes "Shop where you can";
	mes "purchase Apples or grind";
	mes "them to make Apple Juice.";
	next;
	switch (select("Buy Apples.:Make Apple Juice.:Cancel.")) {
	case 1:
		mes "[Meltz]";
		mes "Please enter the amount";
		mes "of Apples that you wish to";
		mes "buy. Each Apple is 15 zeny";
		mes "and you can buy a maximum";
		mes "of 500 at a time. Please enter";
		mes "'0' to cancel your order.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Thanks for stopping";
				mes "by my shop. Farewell!";
				mes "Come by anytime when";
				mes "you feel like having an";
				mes "Apple to snack on~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "You've entered a number";
				mes "higher than the maximum";
				mes "value of 500. Please enter";
				mes "the number of Apples you";
				mes "wish to purchase again.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "A total of ^FF0000" + .@input + "^000000 Apples";
				mes "will cost you ^FF0000" + .@pay + "^000000 zeny.";
				mes "Would you like to continue?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Meltz]";
					mes "Thanks for stopping";
					mes "by my shop. Farewell!";
					mes "Come by anytime when";
					mes "you feel like having an";
					mes "Apple to snack on~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "I'm sorry, you don't have";
			mes "enough money with you.";
			mes "Please check your funds or";
			mes "purchase less Apples.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, I don't think you've";
			mes "got enough room to carry";
			mes "this many Apples. You might";
			mes "want to free up your inventory";
			mes "space.";
			close;
		}
		else {
			Zeny -= .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Thanks for stopping by";
			mes "my shop. I hope you enjoy";
			mes "the flavor of these Apples~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Okay, I'll need";
		mes "^FF00003 Apples and 1 Empty Bottle^000000";
		mes "to make 1 Apple Juice for you.";
		mes "Would you like to proceed?";
		next;
		switch (select("Yes:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "I'm sorry, but you don't";
				mes "have enough materials to";
				mes "create a bottle of Apple Juice.";
				mes "Remember, I need 3 Apples";
				mes "and 1 Empty Bottle to do it.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Thank you, please wait.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Meltz]";
				mes "There you go~";
				mes "Please come again.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Thanks for stopping";
			mes "by my shop. Farewell!";
			mes "Come by anytime when";
			mes "you feel like having an";
			mes "Apple to snack on~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Thanks for stopping";
		mes "by my shop. Farewell!";
		mes "Come by anytime when";
		mes "you feel like having an";
		mes "Apple to snack on~";
		close;
	}
}

airplane_01,221,158,2	script	조타수#airplane_01	4_M_EIN_SOLDIER,{
	switch (rand(1,4)) {
	case 1:
		mes "[조타수]";
		mes "어디 가서 술이라도 한 잔 했으면 소원이 없겠네... 술은 나의 인생! 술은 나의 힘! 술 없는 인생은 앙꼬 없는 찐빵이라네~!";
		next;
		mes "[조타수]";
		mes "하지만 음주운전은 곤란해. 그래서 더욱 괴로워~";
		close;
	case 2:
		mes "[조타수]";
		mes "오늘은 날씨가 정말 좋네요.";
		mes "이런 날은 정말 비공정을 조종할 맛이 나죠.";
		close;
	case 3:
		mes "[조타수]";
		mes "우리 선장님께서는 슈발츠발드 내에서 인품과 격식을 두루 갖추신 걸로 유명하신 분입니다. 그래서 선원들 모두 선장님을 잘 따르고 존경하죠.";
		next;
		mes "[조타수]";
		mes "그 형님 성격이 좀 괴팍하시긴 하지만, 그런 형님을 선장님께선 무척이나 좋아하고 따르신답니다. 그런데 그 형님이 선장님께 무슨 물건을 빌리신 거 같더군요. 선장님이 급히 찾으시던데...";
		close;
	default:
		mes "[조타수]";
		mes "후우...이 제복 멋지긴 한데, 너무 더워... 선장실엔 사람도 안 오는데, 팬티만 입고 일하면 안되나?";
		next;
		mes "[조타수]";
		mes "......";
		next;
		mes "[조타수]";
		mes "헉!? 누구세요!!";
		close;
	}
}

airplane_01,83,61,2	script	소 녀#01airplane_01	1_F_MARIA,2,2,{
	mes "[소 녀]";
	mes "우에에에에엥.......";
	mes "밖에서 뛰어노는데, 웬 사슴이 갑자기 나한테 화를 내쪄. 얼마나 놀랐는데.. 훌쩍";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	귀족신사#01airplane_01	1_M_JOBTESTER,{
	mes "[귀족신사]";
	mes "역시 기내식은 나의 입맛에 맞지 않는군. 음식에 기품도 없고, 센스도 없어. 내 전용 요리사를 오늘 데리고 올 걸 그랬나...";
	close;
}

airplane_01,71,31,2	script	검 사#air_01	1_M_MOC_LORD,{
	mes "[검 사]";
	mes "......5년 전의 복수......";
	mes "......기다려라......";
	mes "이번엔 놓치지 않겠다!!";
	close;
}

//============================================================
//= Typing Challenge
//============================================================
airplane_01,32,61,4	script	닐 스#ein	1_M_03,1,1,{
	mes "[닐 스]";
	mes "안녕하세요. ^ff0000타이핑 오브 더 라그온^000000의 진행자 닐스입니다. 무엇을 도와드릴까요?";
	next;
	switch (select("^ff0000타이핑 오브 더 라그온^000000에 도전한다:^ff0000타이핑 오브 더 라그온^000000에 대한 설명을 듣는다:현재 최고 기록을 본다:취소")) {
	case 1:
		mes "[닐 스]";
		mes "^ff0000타이핑 오브 더 라그온^000000에 도전하시는군요. 주어진 문장을 빠른 시간 안에 정확하게 입력해 주세요. 그럼 시작합니다.";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		.@wordtest = rand(7);
		next;
		mes "[닐 스]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		.@start_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		.@end_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		.@total_time = .@end_time - .@start_time;
		mes "[닐 스]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		.@start_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		.@end_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		.@total_time = .@total_time + (.@start_time - .@end_time);
		.@tasoo = (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[닐 스]";
			mes "^ff0000" + .@total_time + " 초^000000만에 정확하게 입력하셨군요.";
			mes "타수는 " + .@tasoo + "타 나왔습니다.";
			next;
			if (.@tasoo >= 1300) {
				mes "[닐 스]";
				mes "Hmmm, this record isn't";
				mes "humanly possible unless you";
				mes "copy and paste the whole";
				mes "sentence. Please play fairly";
				mes "next time.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[닐 스]";
				mes "The previous top record was";
				mes "made by ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "with the total ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "However, ^ff0000" + strcharinfo(0) + "^000000님,";
				mes "you made the new top record";
				mes "this time. Congratulations!";
				$050320_minus1_typing$ = strcharinfo(0);
				$050320_ein_typing = .@tasoo;
				close;
			}
			else {
				mes "[닐 스]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[닐 스]";
			mes "Oooh...";
			mes "I'm sorry, but";
			mes "you entered the";
			mes "text incorrectly...";
			close;
		}
	case 2:
		mes "[닐 스]";
		mes "^ff0000타이핑 오브 더 라그온^000000은 제가 제시하는 문장을 정확하고 빠르게 키보드로 입력하는 게임입니다. 가장 빠른 속도를 기록하신 분의 이름과 그 속도가 기록되니, 속도에 자신있으신 분은 도전해보세요!";
		close;
	case 3:
		mes "[닐 스]";
		mes "현재 최고 기록은 ^0000ff" + $050320_minus1_typing$ + "^000000이 세우신";
		mes "^0000ff" + $050320_ein_typing + "타^000000가 가장 빠른 기록입니다.";
		close;
	case 4:
		mes "[닐 스]";
		mes "기회는 이번 뿐만이 아닙니다. ^ff0000타이핑 오브 더 라그온^000000에 도전하시려면 언제든지 절 찾아오세요!";
		close;
	}
}

//============================================================
//= Apple Gambling
//============================================================
airplane_01,33,68,4	script	클라리스	1_F_MERCHANT_02,{
	mes "[클라리스]";
	mes "안녕하세요. 제 이름은 클라리스라고 합니다. 가만히 비공정에만 있으니 심심하시죠. 저와 가볍게 주사위 게임 한 판 하실래요?";
	next;
	callfunc "applegamble","클라리스";
	end;
}

function	script	applegamble	{

	switch (select("주사위 게임을 한 판 한다.:주사위 게임의 룰을 배운다.:취소")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "언제든 게임을 하시려면 저에게 말을 걸어주세요. 저도 그냥 있기엔 상당히 심심하니까요. 그럼 이만.";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "주사위 게임의 룰을 설명해드리죠. 아주 간단합니다. 우선 게임을 시작하면, 걸 사과를 최대 50개까지, 제게 맡기시게 됩니다. 물론 사과가 없으면 게임에 참가할 수 없죠.";
		next;
		mes "["+getarg(0)+"]";
		mes "그 다음은 서로 주사위를 두 번 굴리는 거예요. 거기서 나온 수의 합이 큰 쪽이 이기는 거죠. 그래도 한번에 결정나는 건 재미 없으니까, 한 번 정도는 더 굴릴 기회를 양쪽에 줍니다.";
		next;
		mes "["+getarg(0)+"]";
		mes "한 번 더 굴릴지 안 굴릴지는 본인의 자유이지만, 대신 한 번 더 굴려서 숫자의 합이 12가 넘어가면 넘어간 쪽의 패배입니다.";
		next;
		mes "["+getarg(0)+"]";
		mes "세 번째 주사위를 굴릴 때는 손님이 먼저 굴리시는데, 먼저 굴려서 12가 넘어가면 제가 굴리지 않아도 자동으로 제 승리가 됩니다.";
		next;
		mes "["+getarg(0)+"]";
		mes "게임에서 이긴 쪽은 처음 걸었던 사과를 자신의 것과 상대편의 것 모두 갖는 거죠. 이 정도면 충분한 설명이 됐겠죠.";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "주사위 게임을 하기 위해선 사과가 필요합니다. 얼마만큼의 사과를 걸고 게임을 하시겠습니까? 최대 50개까지 걸 수 있으며, 게임을 취소하고 싶으시면 0을 입력하세요.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "마음이 바뀌셨나 보군요. 나중에 심심하시면 다시 찾아주세요. 그럼 이만.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "입력 값을 초과하셨네요. 다시 입력해주세요.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "입력하신 사과의 갯수는 ^FF0000"+.@amount+"^000000 입니다. 맞습니까?";
		next;
		if (select("예:아니오") == 2) {
			mes "["+getarg(0)+"]";
			mes "다시 입력해주세요.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "입력 값을 초과하셨네요. 다시 입력해주세요.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "네. 그럼 게임을 시작하도록 하죠. 제가 먼저 주사위를 굴리겠습니다.";
		break;
	}
	mes "^3355FF*스르륵...데굴데굴*^000000";
	next;
	.@giveapple = .@amount*2;
	.@table1 = rand(1,6);
	.@table2 = rand(1,6);
	.@tablesub = .@table1 + .@table2;
	.@tabletotal = .@tablesub;
	mes "["+getarg(0)+"]";
	mes "전 숫자 ^0000FF" + .@table1 + "^000000, ^0000FF" + .@table2 + "^000000 두 개가 나왔고,";
	mes "합계는 ^0000FF" + .@tablesub + "^000000입니다.";
	mes "이번엔 ^FF0000" + strcharinfo(0) + "^000000님이 굴리세요.";
	next;
	select("주사위를 굴린다.");
	mes "^3355FF*스르륵...데굴데굴...뎅뎅*^000000";
	.@player1 = rand(1,6);
	.@player2 = rand(1,6);
	.@playersub = .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		.@player1 = rand(1,6);
		.@player2 = rand(1,6);
		.@playersub = .@player1 + .@player2;
	}
	.@playertotal = .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000님은 숫자 ^FF0000" + .@player1 + "^000000, ^FF0000" + .@player2 + "^000000 두 개가 나왔네요. 합계는 ^FF0000" + .@playersub + "^000000 입니다.";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "현재 제 합은 ^0000FF" + .@tablesub + "^000000이고, ^FF0000" + strcharinfo(0) + "^000000님의 합은 ^FF0000" + .@playersub + "^000000입니다. 승부가 나질 않았네요. 한 번 더 굴리시겠습니까?";
	} else if (.@playersub > .@tablesub) {
		mes "현재 제 합은 ^0000FF" + .@tablesub + "^000000이고, ^FF0000" + strcharinfo(0) + "^000000님의 합은 ^FF0000" + .@playersub + "^000000입니다. ^FF0000" + strcharinfo(0) + "^000000님이 앞서고 계시네요. 한 번 더 굴리시겠습니까?";
	} else if(.@tablesub > .@playersub) {
		mes "현재 제 합은 ^0000FF" + .@tablesub + "^000000이고, ^FF0000" + strcharinfo(0) + "^000000님의 합은 ^FF0000" + .@playersub + "^000000입니다. 제가 앞서고 있네요. 한 번 더 굴리시겠습니까?";
	}
	next;
	switch (select("굴린다.:굴리지 않는다.")) {
	case 1:
		mes "^3355FF*스르륵...데데굴..데구르르르..삐!*^000000";
		.@player3 = rand(1,6);
		.@playertotal += .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "이번에 나온 ^FF0000" + strcharinfo(0) + "^000000님의 주사위는 숫자 ^FF0000" + .@player3 + "^000000, 합계는 ^FF0000" + .@playertotal + "^000000입니다. 님이 지셨네요. 아쉽지만 다음에 또 이용해주세요. 그럼 이만.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "이번에 나온 ^FF0000" + strcharinfo(0) + "^000000님의 주사위는 숫자 ^FF0000" + .@player3 + "^000000, 합계는 ^FF0000" + .@playertotal + "^000000입니다. 주사위를 던질 필요도 없네요. 여전히 숫자가 큰 제가 이겼습니다. 아쉽지만 다음에 또 이용해주세요. 그럼 이만";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "이번에 나온 ^FF0000" + strcharinfo(0) + "^000000님의 주사위는 숫자 ^FF0000" + .@player3 + "^000000, 합계는 ^FF0000" + .@playertotal + "^000000입니다. 굳이 위험을 감수하면서까지 주사위를 굴릴 필요는 없겠죠. 무승부로 끝났습니다. 다음에 또 이용해주세요.";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
		}
		else {
			mes "^이번에 나온 FF0000" + strcharinfo(0) + "^000000님의 주사위는 숫자 ^FF0000" + .@player3 + "^000000 합계는 ^FF0000" + .@playertotal + "^000000입니다. 이번엔 제가 굴려보죠.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "이기고 계신데 굳이 위험을 감수하면서까지 주사위를 굴릴 필요는 없겠죠. 그럼 이번엔 제가 굴려보겠습니다.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "굳이 위험을 감수하면서까지 주사위를 굴릴 필요는 없겠죠. 무승부로 끝났습니다. 다음에 또 이용해주세요";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "좋아요.";
			mes "다시 한번 주사위를 굴려보죠.";
		}
		else {
			mes "이번엔 제가 이겼네요.";
			mes "아쉽지만 다음에 또 이용해주세요.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*스르륵...데굴굴..데굴구르르..탁*^000000";
	.@table3 = rand(1,6);
	.@tabletotal += .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "이번에 나온 제 주사위 숫자는 ^0000FF" + .@table3 + "^000000, 합계는 ^0000FF" + .@tabletotal + "^000000입니다. 합계 12를 넘겨 제 패배네요. 사과를 드리겠습니다. 다음에 또 한 판 부탁드려요.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "이번에 나온 제 주사위 숫자는 ^0000FF" + .@table3 + "^000000, 합계는 ^0000FF" + .@tabletotal + "^000000입니다. 합계가 ^FF0000" + .@playertotal + "^000000인 ^FF0000" + strcharinfo(0) + "^000000님이 이기셨네요. 사과를 드리겠습니다. 다음에 또 한 판 부탁드려요.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "이번에 나온 제 주사위 숫자는 ^0000FF" + .@table3 + "^000000, 합계는 ^0000FF" + .@tabletotal + "^000000입니다. 합계가 ^FF0000" + .@playertotal + "^000000으로 무승부입니다. ^FF0000" + strcharinfo(0) + "^000000님 사과를 돌려드리겠습니다. 다음에 또 한 판 부탁드려요.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "이번에 나온 제 주사위 숫자는 ^0000FF" + .@table3 + "^000000, 합계는 ^0000FF" + .@tabletotal + "^000000입니다. 합계가 ^FF0000" + .@playertotal + "^000000인 ^FF0000" + strcharinfo(0) + "^000000님이 지셨네요. 아쉽지만 다음에 또 이용해주세요. 그럼 이만.";
		close;
	}
}
