//===== Hercules Script ======================================
//= Einbroch Airport Staff
//===== By: ==================================================
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Einbroch Airport Staff
//===== Additional Comments: =================================
//= Official NPCs.
//= 1.1 kRO Official Texts. [nyalin]
//============================================================

airport,143,43,5	script	공항 직원#airport1a::airport1	4_F_01,{
	mes "[공항 직원]";
	mes "안녕하세요. 저희 항공은 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 무엇을 도와드릴까요?";
	next;
	if (select("공항 내부로 들어간다:취소") == 1) {
		mes "[공항 직원]";
		mes "비공정의 이용 요금은 1200 z 입니다. 비공정 무료이용권을 갖고 계시면 무료로 들어가실 수 있습니다. 공항 내부로 들어 가시겠습니까?";
		next;
		if (select("예:아니오") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				Zeny -= 1200;
				warp "airport",148,51;
				end;
			}
			mes "[공항 직원]";
			mes "지불하실 요금이 부족합니다.";
			close;
		}
	}
	mes "[공항 직원]";
	mes "다음에 또 이용해주세요. 감사합니다.";
	close;
}

airport,158,43,5	duplicate(airport1)	공항 직원#airport1b	4_F_01
airport,126,43,5	duplicate(airport1)	공항 직원#airport1c	4_F_01

airport,143,49,3	script	공항 직원#airport2a::airport2	4_F_01,{
	mes "[공항 직원]";
	mes "안녕하세요. 저희 항공은 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 무엇을 도와드릴까요?";
	next;
	if (select("공항 내부에서 나간다:취소") == 1) {
		mes "[공항 직원]";
		mes "내부에서 나갔다가 들어오시려면 요금을 지불하셔야 합니다. 나가시겠습니까?";
		next;
		if (select("예:아니오") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[공항 직원]";
	mes "다음에 또 이용해주세요. 감사합니다.";
	close;
}

airport,126,51,3	duplicate(airport2)	공항 직원#airport2b	4_F_01
airport,158,50,3	duplicate(airport2)	공항 직원#airport2c	4_F_01

einbroch,94,267,3	script	비공정 직원#ein01	4_F_02,{
	mes "[비공정 직원]";
	mes "안녕하세요. 저희 비공정에선 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 비공정을 타시려면 이쪽 문을 통해 들어가시고, 공항으로 나가시려면 계단을 통해 내려가시면 됩니다.";
	close;
}
