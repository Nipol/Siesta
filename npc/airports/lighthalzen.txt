//===== Hercules Script ======================================
//= Lighthalzen Airport Staff
//===== By: ==================================================
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Lighthalzen Airport Staff
//===== Additional Comments: =================================
//= Official NPCs.
//= 1.1 kRO Official Texts. [nyalin]
//============================================================

lhz_airport,143,43,5	script	공항 직원#lhz_air1a::lhz_airport1	4_F_01,{
	mes "[공항 직원]";
	mes "안녕하세요. 저희 항공은 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 무엇을 도와드릴까요?";
	next;
	if (select("공항 내부로 들어간다:취소") == 1) {
		mes "[공항 직원]";
		mes "비공정의 이용 요금은 1200 z 입니다. 비공정 무료이용권을 갖고 계시면 무료로 들어가실 수 있습니다. 공항 내부로 들어 가시겠습니까?";
		next;
		if (select("예:아니오") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "lhz_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				Zeny -= 1200;
				warp "lhz_airport",148,51;
				end;
			}
			mes "[공항 직원]";
			mes "지불하실 요금이 부족합니다.";
			close;
		}
	}
	mes "[공항 직원]";
	mes "다음에 또 이용해주세요. 감사합니다.";
	close;
}

lhz_airport,158,43,5	duplicate(lhz_airport1)	공항 직원#lhz_air1b	4_F_01
lhz_airport,126,43,5	duplicate(lhz_airport1)	공항 직원#lhz_air1c	4_F_01

lhz_airport,143,49,3	script	공항 직원#lhz_air2a::lhz_airport2	4_F_01,{
	mes "[공항 직원]";
	mes "안녕하세요. 저희 항공은 항상 고객님의 편안한 여행을 위해 최선을 다하고 있습니다. 무엇을 도와드릴까요?";
	next;
	if (select("공항 내부에서 나간다:취소") == 1) {
		mes "[공항 직원]";
		mes "내부에서 나갔다가 들어오시려면 요금을 지불하셔야 합니다. 나가시겠습니까?";
		next;
		if (select("예:아니오") == 1) {
			warp "lhz_airport",142,40;
			end;
		}
	}
	mes "[공항 직원]";
	mes "다음에 또 이용해주세요. 감사합니다.";
	close;
}

lhz_airport,126,51,3	duplicate(lhz_airport2)	공항 직원#lhz_air2b	4_F_01
lhz_airport,158,50,3	duplicate(lhz_airport2)	공항 직원#lhz_air2c	4_F_01
