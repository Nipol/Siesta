//===== Hercules Script ======================================
// BattleGround System - Common NPCs
//===== By: ==================================================
//= ????, L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Description: =========================================
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC (disabled by default)
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: =================================
//= kRO Official Texts & Updated to match the kRO script. [nyalin]
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//============================================================

// Generals
//============================================================
bat_room,160,159,3	script	장군 기욤 마롤로	4_M_KY_KIYOM,{
	cutin "bat_kiyom2",2;
	mes "[기욤 마롤로]";
	mes "혈기왕성한 모험가여, 우리들은 그대의 잠재된 능력을 필요로 하고 있다.";
	next;
	switch(select("전투의 이유에 대하여:크루아 마롤로에 대하여")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[기욤 마롤로]";
		mes "우리들의 고귀하신 폐하, 마르셀 마롤로 7세의 건강이 위독하시다.";
		mes "폐하는 우리 9명의 왕자들 중 나와 크루아에게 왕위 계승의 뜻을 밝히셨지.";
		next;
		mes "[기욤 마롤로]";
		mes "하지만 두 명의 왕이 있을 수는 없는 법! 폐하께서 지목하신 전장에서 승리를 거둔 자만이 이 나라의 통치권을 쥐게되는 것이다.";
		next;
		mes "[기욤 마롤로]";
		mes "이것은 이미 우리들만의 싸움이 아니야. 이 전투의 결과에 따라 이 나라의 미래가 뒤흔들릴터.";
		mes "장군의 명예를 걸고 마롤로를 외부의 위협으로부터 안전하게 지켜낼 수 있는 사람은 나라는 걸 증명해 보이겠어.";
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[기욤 마롤로]";
		mes "제 5왕자 크루아 마롤로는 현재 마롤의 집사장관을 맡고 있는 자이지.";
		mes "국가의 정세, 위상이라는 것이 책상에만 앉아 처리할 수 있을 만큼 호락호락한 줄 아나?";
		next;
		mes "[기욤 마롤로]";
		mes "거기다 평화 정책이라고?";
		mes "하하! 평화로운 세상을 유지하기 위해 보이지 않는 곳에서 얼마나 많은 이가 피를 흘려야 하는지 그 녀석은 모르는가 보군.";
		next;
		mes "[기욤 마롤로]";
		mes "그 녀석은 우물 안 개구리일 뿐인 애송이야.";
		mes "평화 밖에 모르는 철부지 크루아에게 마롤을 맡길 수는 없다.";
		next;
		mes "[기욤 마롤로]";
		mes "그 녀석의 안이한 생각은 나라를 가난하게 만들고 국민들을 약해 빠지게 만들며, 점차 주변 국가들의 멋잇감으로 만들 뿐이다.";
		mes "이 작은 땅덩이를 가진 마롤이 이만큼 번창 할 수 있었던 것은 강대한 군사력이 뒷받침 되었기 때문이다.";
		break;
	}
	next;
	mes "[기욤 마롤로]";
	mes "나는 반드시 승리의 깃발을 거머쥘 것이다!";
	close2;
	cutin "",255;
	end;
}

bat_room,161,158,3	script	기욤 보좌관#01	4_M_KY_HEAD,{ end; }
bat_room,161,160,3	script	기욤 보좌관#03	4_M_KY_HEAD,{ end; }

bat_room,160,141,3	script	크루아 마롤로	4_M_CRU_CRUA,{
	cutin "bat_crua1",2;
	mes "[크루아 마롤로]";
	mes "지혜로운 모험가시여, 우리에게 당신의 그 힘을 빌려주시지 않겠습니까?";
	next;
	switch(select("전투의 이유에 대하여:기욤 마롤로에 대하여")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[크루아 마롤로]";
		mes "마롤의 위대한 국왕 마르셀 마롤로 7세의 용태가 매우 좋지 않은 상황입니다.";
		mes "폐하께선 제 3왕자 기욤 마롤로 장군과 저 중 한명에게 마롤의 미래를 맡기겠다 하셨습니다.";
		next;
		mes "[크루아 마롤로]";
		mes "전투는 이미 마롤의 장군으로 활약중인 기욤 마롤로에게 유리하다 할 수 있을지 모르겠으나, 그렇다고 질 생각은 결코 없습니다.";
		mes "저는 이 승부에서 반드시 이겨, 마롤의 국민들을 전쟁의 상처에서 구해낼 것입니다.";
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[크루아 마롤로]";
		mes "제 3왕자 기욤 마롤로는 마롤의 장군입니다.";
		mes "강력한 군사력을 자랑하는 마롤의 장군이니만큼 그의 전투에 대한 지휘력과 무예 능력은 설명할 필요도 없죠.";
		next;
		mes "[크루아 마롤로]";
		mes "그러나 마롤의 군사력에 의지하는 자들이 간과하고 있는게 있습니다.";
		mes "과연 백성들도 그 군사력을 원하는가?";
		mes "그 동안 마롤의 국민들은 수많은 전쟁으로 상처 받아 왔습니다.";
		mes "강한 칼날은 언젠가 부서지기 마련입니다.";
		next;
		mes "[크루아 마롤로]";
		mes "저는 피를 흘리는 것이 싫지만 더 이상 많은 피를 흘리지 않기 위해 이번만큼은 절대 지지 않겠습니다.";
		break;
	}
	next;
	mes "[크루아 마롤로]";
	mes "마롤을 위하여!";
	close2;
	cutin "",255;
	end;
}

bat_room,161,140,3	script	크루아 보좌관#01	4_M_CRU_HEAD,{ end; }
bat_room,161,142,3	script	크루아 보좌관#02	4_M_CRU_HEAD,{ end; }

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	1_FLAG_LION,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	1_FLAG_LION,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	1_FLAG_EAGLE,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	1_FLAG_EAGLE,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	공간이동사#Battlefield	4_F_TELEPORTER,{
	mes "[공간이동사]";
	mes "이곳을 벗어나고 싶으십니까? 저를 통해 당신이 머물렀던 곳으로 다시 돌아가실 수 있습니다.";
	next;
	switch(select("마을로 돌아간다.:돌아가지 않는다.")) {
	case 1:
		mes "[공간이동사]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"프론테라로","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"모로크로","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"알데바란으로","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"게펜으로","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"페이욘으로.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"리히타르젠으로","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"라헬로","rachel";
			setarray .@xy[0],115,124;
			break;
		}
		mes "당신을 "+.@mapname$[0]+" 돌려보냅니다.";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[공간이동사]";
		mes "필요하실 땐 언제든지 저를 불러주세요.";
		close;
	}
	end;
}

-	script	마롤 전장 모병관::BatRecruit	4_F_JOB_KNIGHT,{
	mes "[마롤 전장 모병관]";
	mes "안녕하십니까 모험가시여.";
	mes "저는 저 멀고 먼 마롤왕국 출신의 기사입니다.";
	next;
	mes "[마롤 전장 모병관]";
	mes "두 명의 왕자가 마롤의 왕위계승건으로 전투의 장을 벌이게 되어, 당신과 같은 경험 많은 모험가를 상대로 용병을 모집하고 있습니다.";
	mes "한 번 당신의 그 힘을 우리 마롤에 빌려주시지 않겠습니까?";
	next;
	switch(select("전장에 참여한다.:참여하지 않는다.")) {
	case 1:
		mes "[마롤 전장 모병관]";
		mes "전투와 용기의 신의 가호가 언제나 당신과 함께하기를..";
		close2;
		getmapxy(.@mapname$,.@x,.@y,1);
		if (.@mapname$ == "prontera")
			bat_return = 1;
		else if (.@mapname$ == "moc_ruins")
			bat_return = 2;
		else if (.@mapname$ == "aldebaran")
			bat_return = 3;
		else if (.@mapname$ == "geffen")
			bat_return = 4;
		else if (.@mapname$ == "payon")
			bat_return = 5;
		else if (.@mapname$ == "lighthalzen")
			bat_return = 6;
		else if (.@mapname$ == "rachel")
			bat_return = 7;
		else
			bat_return = 1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[마롤 전장 모병관]";
		mes "저는 항상 이 곳에서 대기하고 있으니, 관심이 생긴다면 다시 찾아와 주십시오.";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit1	4_F_JOB_KNIGHT
moc_ruins,75,162,3	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit2	4_F_JOB_KNIGHT
aldebaran,146,109,3	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit3	4_F_JOB_KNIGHT
geffen,109,66,3	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit4	4_F_JOB_KNIGHT
payon,189,105,3	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit5	4_F_JOB_KNIGHT
lighthalzen,153,86,5	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit6	4_F_JOB_KNIGHT
rachel,149,138,3	duplicate(BatRecruit)	마롤 전장 모병관::BatRecruit7	4_F_JOB_KNIGHT

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	카프라 직원::kaf_bat	4_F_KAFRA9,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman (kRO 사용되지 않음)
//============================================================
bat_room,138,144,4	script	수리공#bg	4_M_04,{
	callfunc "repairmain","수리공";
	end;
}

// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	4_DOG01,{
	.@i = callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "The command has been cancelled.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "May I help you?";
		next;
		switch(select("Close Battlefield:Open Battlefield:Reset a01:Reset b01:Reset a02:Reset b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Complete";
		close;
	}
}

// Badges Exchange
//============================================================
bat_room,160,150,3	script	에룬데크	4_M_MANAGER,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you loose some weight. -";
		close;
	}
	mes "[에룬데크]";
	mes "혹시 전장을 통해 습득하신 휘장을 가지고 계십니까?";
	mes "저는 특공휘장과 용맹휘장을 아이템으로 바꿔드리는 일을 하고 있습니다.";
	next;
	switch(select("휘장을 교환한다:카탈로그를 본다.")) {
	case 1:
		mes "[에룬데크]";
		mes "어느 물건들을 보시겠습니까?";
		mes "아이템들의 자세한 성능은 ^3131FF카탈로그^000000를 통해 확인해 주시기 바랍니다.";
		next;
		switch(select("무기:방어구:액세서리:소비아이템")) {
		case 1:
			mes "[에룬데크]";
			mes "^3131FF무기^000000를 선택하셨습니다..";
			mes "휘장으로 교환하실 수 있는 무기는 다음과 같은 종류가 있습니다.";
			mes "^3131FF용맹휘장전용은 (용), 특공휘장전용은 (특)^000000으로 구분되니 참고하시기 바랍니다.";
			next;
			switch(select("단검/한손검/양손검/양손창:지팡이/둔기/양손도끼/풍마수리검:활/카타르/악기/채찍:책/너클:리볼버/라이플/개틀링건/샷건/그레네이트런쳐")) {
			case 1:
				mes "[에룬데크]";
				mes "저희가 준비한 ^3131FF단검/한손검/양손검/양손창^000000에는 다음과 같은 아이템들이 있습니다.";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[에룬데크]";
				mes "저희가 준비한 ^3131FF지팡이/둔기/양손도끼/풍마수리검^000000에는 다음과 같은 아이템들이 있습니다.";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[에룬데크]";
				mes "저희가 준비한 ^3131FF활/카타르/악기/채찍^000000에는 다음과 같은 아이템들이 있습니다.";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[에룬데크]";
				mes "저희가 준비한 ^3131FF책/너클^000000에는 다음과 같은 아이템들이 있습니다.";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[에룬데크]";
				mes "저희가 준비한 ^3131FF리볼버/라이플/개틀링건/샷건/그레네이트런쳐^000000에는 다음과 같은 아이템들이 있습니다.";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			.@menu$ = "";
			for (.@i = 0; .@i < getarraysize(.@Weapons); .@i += 2)
				.@menu$ += getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
			.@i = (select(.@menu$)-1)*2;
			.@type$ = ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[에룬데크]";
			mes "^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000을 선택하셨습니다.";
			mes "이 아이템은 ^FF0000"+getitemname(.@Weapons[.@i+1])+" 100개^000000로 교환하실 수 있습니다.";
			mes "교환하시겠습니까?";
			next;
			switch(select("교환하지 않는다.:교환한다.")) {
			case 1:
				break;
			case 2:
				mes "[에룬데크]";
				mes "^FF0000"+getitemname(.@Weapons[.@i+1])+" 100개^000000로 ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000을 구입하시겠습니까?";
				next;
				switch(select("네:아니오")) {
				case 1:
					mes "[에룬데크]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "Thank you for exchanging.";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "교환에 필요한 휘장의 갯수가 부족합니다.";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[에룬데크]";
			mes "좀 더 살펴보시겠습니까?";
			close;
		case 2:
			mes "[에룬데크]";
			mes "^3131FF방어구^000000를 선택하셨습니다.";
			mes "휘장으로 교환하실 수 있는 방어구는 다음과 같은 종류가 있습니다.";
			next;
			switch(select("걸칠것/신발:갑옷")) {
			case 1:
				setarray .@items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[에룬데크]";
			mes "^3131FF액세서리^000000를 선택하셨습니다.";
			mes "휘장으로 교환하실 수 있는 무공훈장들은 다음과 같은 직업 계열 별로 나뉩니다.";
			next;
			setarray .@items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			.@menu1$ = "건슬링거:검사/권성:도둑:복사:마법사:궁수:상인";
			break;
		case 4:
			mes "[에룬데크]";
			mes "^3131FF소비아이템^000000을 선택하셨습니다.";
			mes "휘장으로 교환하실 수 있는 소비아이템은 다음과 같은 종류가 있습니다.";
			next;
			setarray .@items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[에룬데크]";
		mes "이렇게 많은 아이템들이 준비되어 있으니, 천천히 살펴 보시고 신중히 구입하시기 바랍니다.";
		close2;
		readbook 11010,1;
		end;
	}
	.@menu$ = "";
	if (.@menu1$ != "") .@menu$ = .@menu1$;
	else for (.@i = 0; .@i < getarraysize(.@items); .@i += 2)
		.@menu$ += getitemname(.@items[.@i])+":";
	.@i = (select(.@menu$)-1)*2;
	mes "[에룬데크]";
	mes "^3131FF"+getitemname(.@items[.@i])+"^000000를 선택하셨습니다.";
	switch(.@items[.@i]) {
		case 2720: mes "검사 계열 전용 아이템 입니다."; break;
		case 2721: mes "도둑 계열 전용 아이템 입니다."; break;
		case 2722: mes "복사 계열 전용 아이템 입니다."; break;
		case 2723: mes "마법사 계열 전용 아이템입니다."; break;
		case 2724: mes "궁수 계열 전용 아이템 입니다."; break;
		case 2725: mes "상인 계열 전용 아이템 입니다."; break;
		case 2733: mes "건슬링거 전용 아이템입니다."; break;
		default: break;
	}
	mes "이 아이템은 ^FF0000용맹 혹은 특공휘장 "+.@items[.@i+1]+"^000000로 교환하실 수 있습니다.";
	mes "교환하시겠습니까?";
	next;
	switch(select("교환하지 않는다.:교환한다.")) {
	case 1:
		mes "[에룬데크]";
		mes "좀 더 살펴보시겠습니까?";
		break;
	case 2:
		mes "[에룬데크]";
		mes "어떤 휘장과 교환하시겠습니까?";
		mes "교환에 필요한 갯수는 ^3131FF"+.@items[.@i+1]+"개^000000 입니다.";
		next;
		.@j = select("용맹휘장:특공휘장:교환을 취소한다.");
		mes "[에룬데크]";
		if (.@j == 3) {
			mes "교환을 취소하였습니다.";
			break;
		}
		.@cost = ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@items[.@i+1]) {
			mes "Thank you for exchanging.";
			delitem .@cost, .@items[.@i+1];
			getitem .@items[.@i],1;
		}
		else mes "교환에 필요한 "+getitemname(.@cost)+"의 갯수가 부족합니다.";
		break;
	}
	close;
}
